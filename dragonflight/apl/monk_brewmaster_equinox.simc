actions.precombat+=/rushing_jade_wind,if=buff.rushing_jade_wind.remains<gcd
actions.precombat+=/breath_of_fire,if=talent.charred_passions.enabled&buff.charred_passions.down
actions.precombat+=/summon_white_tiger_statue
actions.precombat+=/keg_smash

# Tunable variables
# Minimum number of Purified Chi stacks before using Celestial Brew.
actions+=/variable,name=celestial_brew_purified_chi_threshold,value=6
# Stagger at which you should use Purifying Brew in an emergency.
actions+=/variable,name=purify_stagger_pct,value=20
# Health percent at which to use defensive buffs.
actions+=/variable,name=defensive_health_pct,value=75
actions+=/variable,name=high_incoming_damage_pct,value=20

actions+=/variable,name=energy_cost_keg_smash,value=40
actions+=/variable,name=energy_cost_spinning_crane_kick,value=25
actions+=/variable,name=energy_cost_tiger_palm,value=25
actions+=/variable,name=energy_at_next_keg_smash,value=energy+energy.regen*cooldown.keg_smash.remains
actions+=/variable,name=has_defensive_buff,value=1,op=setif,condition=buff.celestial_brew.up|buff.dampen_harm.up|buff.fortifying_brew.up,value_else=0
actions+=/variable,name=high_incoming_damage_amount,value=health.max*variable.high_incoming_damage_pct%100

actions+=/spear_hand_strike
actions+=/call_action_list,name=heal
actions+=/call_action_list,name=defensives,if=health.pct<variable.defensive_health_pct&variable.has_defensive_buff=0
actions+=/call_action_list,name=mitigation,if=!buff.blackout_combo.up
actions+=/touch_of_death
# Keg Smash on the opener for immediate threat and to activate Shuffle.
actions+=/keg_smash,if=time=0
# Use Weapons of Order early in the opener so subsequent damage abilities all benefit from the stacking debuff on the targets.
actions+=/weapons_of_order,if=time<5
# Only use cooldowns if they won't push back the next Keg Smash.
actions+=/call_action_list,name=cooldowns,if=!talent.stormstouts_last_keg.enabled&cooldown.keg_smash.remains>gcd|cooldown.keg_smash.charges_fractional<1
actions+=/run_action_list,name=core_charred,if=talent.charred_passions.enabled
actions+=/run_action_list,name=core_dragonfire,if=talent.core_dragonfire_brew.enabled
actions+=/call_action_list,name=core

actions.cooldowns+=/call_action_list,name=racials
actions.cooldowns+=/weapons_of_order
actions.cooldowns+=/summon_white_tiger_statue
actions.cooldowns+=/invoke_niuzao_the_black_ox
actions.cooldowns+=/exploding_keg

actions.core+=/rushing_jade_wind,if=buff.rushing_jade_wind.remains<gcd
actions.core+=/call_action_list,name=keg_smash,if=!talent.stormstouts_last_keg.enabled|cooldown.keg_smash.full_recharge_time<gcd
actions.core+=/bonedust_brew,if=talent.press_the_advantage.enabled
actions.core+=/blackout_kick
actions.core+=/rising_sun_kick
actions.core+=/breath_of_fire
actions.core+=/bonedust_brew
actions.core+=/call_action_list,name=keg_smash
actions.core+=/call_action_list,name=sck
actions.core+=/call_action_list,name=filler

actions.core_charred+=/rushing_jade_wind,if=buff.rushing_jade_wind.remains<gcd
actions.core_charred+=/breath_of_fire
actions.core_charred+=/bonedust_brew,if=talent.press_the_advantage.enabled
actions.core_charred+=/blackout_kick
actions.core_charred+=/bonedust_brew
actions.core_charred+=/call_action_list,name=keg_smash,if=buff.hit_scheme.up
actions.core_charred+=/rising_sun_kick,if=active_enemies<5
actions.core_charred+=/call_action_list,name=sck,if=active_enemies>3
actions.core_charred+=/call_action_list,name=tp
actions.core_charred+=/call_action_list,name=keg_smash
actions.core_charred+=/call_action_list,name=filler

actions.core_dragonfire+=/rushing_jade_wind,if=buff.rushing_jade_wind.remains<gcd
actions.core_dragonfire+=/call_action_list,name=keg_smash,if=!talent.stormstouts_last_keg.enabled|cooldown.keg_smash.full_recharge_time<gcd
actions.core_dragonfire+=/bonedust_brew,if=talent.press_the_advantage.enabled
actions.core_dragonfire+=/blackout_kick
actions.core_dragonfire+=/breath_of_fire
actions.core_dragonfire+=/bonedust_brew
actions.core_dragonfire+=/rising_sun_kick,if=active_enemies<=5
actions.core_dragonfire+=/call_action_list,name=keg_smash
actions.core_dragonfire+=/call_action_list,name=sck
actions.core_dragonfire+=/call_action_list,name=filler

actions.defensives+=/fortifying_brew,if=incoming_damage_3s>variable.high_incoming_damage_amount
actions.defensives+=/diffuse_magic,if=incoming_magic_damage_3s>variable.high_incoming_damage_amount
actions.defensives+=/dampen_harm,if=incoming_damage_3s>variable.high_incoming_damage_amount
actions.defensives+=/celestial_brew,if=(!talent.improved_celestial_brew.enabled|buff.purified_chi.stack>=variable.celestial_brew_purified_chi_threshold)&incoming_damage_3s>variable.high_incoming_damage_amount

actions.filler+=/rushing_jade_wind
actions.filler+=/chi_burst
actions.filler+=/chi_wave

actions.heal+=/variable,expel_harm_heal_amount,value=(buff.gift_of_the_ox.stack*stat.attack_power)+(stat.spell_power*(1+stat.versatility_atk_mod%100))
actions.heal+=/expel_harm,if=health.pct<70|(buff.gift_of_the_ox.stack>=3&health+variable.expel_harm_heal_amount<=1.2*health.max)
actions.heal+=/healing_elixir,if=health.pct<(70-30*(max_charges-charges_fractional))

# Try to prefer Breath of Fire over Keg Smash when consuming Blackout Combo.
actions.keg_smash+=/keg_smash,if=buff.blackout_combo.down|cooldown.breath_of_fire.remains>2

actions.mitigation+=/variable,name=gai_plins_heal_amount,value=stagger.amount_remains*0.5*0.25,op=setif,condition=talent.gai_plins_imperial_brew.enabled,value_else=0
actions.mitigation+=/purifying_brew,if=talent.improved_purifying_brew.enabled&full_recharge_time<gcd.max&(stagger.heavy|time>5&health+variable.gai_plins_heal_amount<1.2*health.max)
actions.mitigation+=/purifying_brew,if=talent.improved_celestial_brew.enabled&buff.purified_chi.up&buff.purified_chi.remains<3
actions.mitigation+=/purifying_brew,if=stagger.pct>variable.purify_stagger_pct
actions.mitigation+=/black_ox_brew,if=cooldown.celestial_brew.remains>5&(!talent.improved_purifying_brew.enabled&cooldown.purifying_brew.remains>gcd|cooldown.purifying_brew.charges_fractional<0.75)

actions.racials=blood_fury
actions.racials+=/berserking
#actions.racials+=/arcane_torrent
actions.racials+=/lights_judgment
actions.racials+=/ancestral_call
#actions.racials+=/fireblood
actions.racials+=/bag_of_tricks

# Only cast Spinning Crane Kick or Tiger Palm if it won't push back the next Keg Smash.
# Spinning Crane Kick shouldn't be cast consecutively with Press the Advantage.
actions.sck+=/spinning_crane_kick,if=variable.energy_at_next_keg_smash>variable.energy_cost_keg_smash+variable.energy_cost_spinning_crane_kick&(!talent.press_the_advantage.enabled|!prev_gcd=spinning_crane_kick)
actions.tp+=/tiger_palm,if=variable.energy_at_next_keg_smash>variable.energy_cost_keg_smash+variable.energy_cost_tiger_palm
