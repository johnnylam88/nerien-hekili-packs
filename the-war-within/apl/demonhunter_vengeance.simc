# Vengeance Demon Hunter Priority List
# Based on *Vengeance Demon Hunter Tank Guide* by Meyra
# [https://www.icy-veins.com/wow/vengeance-demon-hunter-pve-tank-guide]
# Adjustments for opener and defensive usage from YoDaTV
# [https://www.youtube.com/watch?v=OoNbob65PYQ]
#
# Recommended Settings for `/hekili` > *Vengeance*:
#
# - Reserve Infernal Strike Charges: 0
# - Reserve Fiery Brand Charges: 0

actions.precombat+=/sigil_of_flame,if=hero_tree.felscarred

### Tunables ###

# Health percent threshold to cast Bulk Extraction for healing.
actions+=/variable,name=bulk_extraction_health_pct_threshold,value=70
# Minimum percent of mobs that must have Fiery Brand to avoid overlapping another defensive.
actions+=/variable,name=fiery_brand_pct_threshold,value=75
# Minimum percent of mobs that must have Sigil of Flame to avoid recasting Sigil of Flame.
actions+=/variable,name=illuminated_sigils_pct_threshold,value=75
# Metamorphosis tunable for incoming damage as Aldrachi Reaver.
actions+=/variable,name=metamorphosis_damage_taken,value=0.2*health.max
# Darkness tunable for incoming damage.
actions+=/variable,name=darkness_damage_taken,value=0.2*health.max

################

# Check whether you are in a multi-target situation.
actions+=/variable,name=num_targets,value=active_enemies,value_else=spell_targets.spirit_bomb,op=setif,condition=!talent.spirit_bomb

actions+=/variable,name=fury_cost_fel_devastation,value=50
actions+=/variable,name=fury_cost_soul_cleave,value=30
actions+=/variable,name=fury_cost_spirit_bomb,value=40
actions+=/variable,name=fury_gain_felblade,value=40
actions+=/variable,name=fury_gain_fracture,value=45,value_else=25,op=setif,condition=buff.metamorphosis.up
actions+=/variable,name=fury_gain_immolation_aura,value=26,value_else=20,op=setif,condition=talent.agonizing_flames
actions+=/variable,name=fury_gain_keen_engagement,value=20,condition=talent.keen_engagement
actions+=/variable,name=fury_gain_shear_base,value=10+(10*talent.shear_fury)
actions+=/variable,name=fury_gain_shear,value=variable.fury_gain_shear_base+20,value_else=variable.fury_gain_shear_base,op=setif,condition=buff.metamorphosis.up
# Flames of Fury causes Sigil of Flame to generate 2 additional Fury per target hit.
actions+=/variable,name=fury_gain_sigil_of_flame,value=30+2*variable.num_targets*talent.flames_of_fury

# immolation_aura_crit is the critical strike percentage for Immolation Aura.
actions+=/variable,name=immolation_aura_crit,value=stat.crit+0.06,value_else=stat.crit,op=setif,condition=talent.aura_of_pain

actions+=/variable,name=fury_check_felblade,value=1,value_else=0,op=setif,condition=talent.felblade&cooldown.felblade.remains<gcd&fury+variable.fury_gain_felblade>fury.max
actions+=/variable,name=fury_check_fracture,value=1,value_else=0,op=setif,condition=talent.fracture&cooldown.fracture.remains<gcd&fury+variable.fury_gain_fracture>fury.max
actions+=/variable,name=fury_check_keen_engagement,value=1,value_else=0,op=setif,condition=talent.keen_engagement&cooldown.reavers_glaive.remains<gcd&fury+variable.fury_gain_keen_engagement>fury.max
# Immolation Aura (and Consuming Fire) generates 8 Fury on use, then 2 Fury per second afterwards.
actions+=/variable,name=fury_check_immolation_aura,value=1,value_else=0,op=setif,condition=cooldown.immolation_aura.remains<gcd&fury+8>fury.max
actions+=/variable,name=fury_check_shear,value=1,value_else=0,op=setif,condition=!talent.fracture&fury+variable.fury_gain_shear>fury.max
# Sigil of Flame (and Sigil of Doom) is only cast if it's ready and there's no active DoT.
actions+=/variable,name=fury_check_sigil_of_flame,value=1,value_else=0,op=setif,condition=active_dot.sigil_of_flame=0&cooldown.sigil_of_flame.charges>0&fury+variable.fury_gain_sigil_of_flame>fury.max
# should_dump_fury is 1 if Fury will cap with the next ability.
actions+=/variable,name=should_dump_fury,value=1,value_else=0,op=setif,condition=variable.fury_check_felblade>0|variable.fury_check_fracture>0|variable.fury_check_immolation_aura>0|variable.fury_check_shear>0|variable.fury_check_sigil_of_flame>0

# Variables to treat souls as a resource.
actions+=/variable,name=souls_max,value=5
actions+=/variable,name=souls_deficit,value=variable.souls_max-soul_fragments.active

actions+=/variable,name=souls_gain_fracture,value=3,value_else=2,op=setif,condition=buff.metamorphosis.up
actions+=/variable,name=souls_gain_immolation_aura,value=0.7*spell_targets.immolation_aura,value_else=0,op=setif,condition=talent.fallout
actions+=/variable,name=souls_gain_shear,value=2,value_else=1,op=setif,condition=buff.metamorphosis.up
actions+=/variable,name=souls_gain_sigil_of_flame,value=1*talent.soul_sigils
actions+=/variable,name=souls_gain_sigil_of_spite,value=3+1*talent.soul_sigils
actions+=/variable,name=souls_gain_soul_carver,value=3
# should_dump_souls is 1 if Soul Fragments are capped.
actions+=/variable,name=should_dump_souls,value=1,value_else=0,op=setif,condition=(variable.num_targets>1&variable.souls_deficit<=1)|variable.souls_deficit=0

# demonsurge_remaining is the number of abilities remaining that can trigger Demonsurge.
actions+=/variable,name=demonsurge_remaining,op=reset,default=0
actions+=/variable,name=demonsurge_remaining,op=add,value=1,if=talent.demonsurge&buff.demonsurge_soul_sunder.up
actions+=/variable,name=demonsurge_remaining,op=add,value=1,if=talent.demonsurge&buff.demonsurge_spirit_burst.up
actions+=/variable,name=demonsurge_remaining,op=add,value=1,if=talent.demonic_intensity&buff.demonsurge_fel_desolation.up
actions+=/variable,name=demonsurge_remaining,op=add,value=1,if=talent.demonic_intensity&buff.demonsurge_consuming_fire.up
actions+=/variable,name=demonsurge_remaining,op=add,value=1,if=talent.demonic_intensity&buff.demonsurge_sigil_of_doom.up

# sigil_activation_duration is the number of seconds before a placed sigil will activate.
actions+=/variable,name=sigil_activation_duration,value=1+talent.quickened_sigils

# has_fiery_brand_safety is 1 if Fiery Brand is on the current target and a sufficient percentage of the enemies.
actions+=/variable,name=has_fiery_brand_safety,value=1,value_else=0,op=setif,condition=debuff.fiery_brand_dot.up&(active_dot.fiery_brand_dot%variable.num_targets*100>=variable.fiery_brand_pct_threshold)

# has_illuminated_sigils_safety is 1 if Sigil of Flame is on the current target and a sufficient percentage of the enemies.
actions+=/variable,name=has_illuminated_sigils_safety,value=1,value_else=0,op=setif,condition=talent.illuminated_sigils&(debuff.sigil_of_flame.up|debuff.casting.down)&(active_dot.sigil_of_flame%variable.num_targets*100>=variable.illuminated_sigils_pct_threshold)

# can_full_uptime_demon_spikes is 1 if we can maintain Demon Spikes with 100% uptime.
actions+=/variable,name=can_full_uptime_demon_spikes,value=1,value_else=0,op=setif,condition=talent.extended_spikes&talent.feed_the_demon&(variable.num_targets>=5)

# ar_fel_devastation_ready is 1 as Aldrachi Reaver if Fel Devastation is off cooldown and either won't overlap Demon Spikes or Fiery Brand, or while damage multipliers are up in single-target.
actions+=/variable,name=ar_fel_devastation_ready,value=1,value_else=0,op=setif,condition=!talent.demonsurge&cooldown.fel_devastation.ready&((!talent.thrill_of_the_fight&!talent.demonic)|(variable.num_targets=1&buff.thrill_of_the_fight_damage.up&(!talent.fiery_demise|debuff.fiery_brand_dot.remains>2))|(talent.demonic&buff.demon_spikes.remains<gcd&variable.has_fiery_brand_safety=0))

# fs_fel_devastation_ready is 1 as Fel-Scarred if Fel Devastation is off cooldown and won't overlap Demon Spikes.
actions+=/variable,name=fs_fel_devastation_ready,value=1,value_else=0,op=setif,condition=talent.demonsurge&buff.metamorphosis.remains<gcd&cooldown.fel_devastation.ready&(!talent.demonic|(variable.demonsurge_remaining=0&buff.demon_spikes.remains<gcd))

# fel_devastation_ready is 1 if Fel Devastation is immiment.
actions+=/variable,name=fel_devastation_ready,value=1,value_else=0,op=setif,condition=variable.ar_fel_devastation_ready>0|variable.fs_fel_devastation_ready>0

# ar_metamorphosis_ready is 1 as Aldrachi Reaver if Metamorphosis is off cooldown and won't overlap other defensive buffs.
actions+=/variable,name=ar_metamorphosis_ready,value=1,value_else=0,op=setif,condition=!talent.demonsurge&incoming_damage_5s>variable.metamorphosis_damage_taken&cooldown.metamorphosis.ready&variable.has_defensive_buff=0&variable.has_fiery_brand_safety=0

# fs_metamorphosis_ready is 1 as Fel-Scarred if Metamorphosis is off cooldown and all spells reset by Violent Transformation are on cooldown.
actions+=/variable,name=fs_metamorphosis_ready,value=1,value_else=0,op=setif,condition=talent.demonsurge&cooldown.metamorphosis.ready&(!cooldown.fel_devastation.ready&cooldown.fel_devastation.remains>10)&variable.demonsurge_remaining=0&(!talent.fiery_demise|cooldown.fiery_brand.charges<cooldown.fiery_brand.max_charges)&cooldown.sigil_of_flame.charges<1

# metamorphosis_ready is 1 if Metamorphosis is immiment.
actions+=/variable,name=metamorphosis_ready,value=1,value_else=0,op=setif,condition=variable.ar_metamorphosis_ready>0|variable.fs_metamorphosis_ready>0

# can_demon_spikes is 1 if Demon Spikes is down, and Metamorphosis is down or Demon Spikes can have 100% uptime.
actions+=/variable,name=can_demon_spikes,value=1,value_else=0,op=setif,condition=buff.demon_spikes.remains<gcd&(buff.metamorphosis.remains<gcd|variable.can_full_uptime_demon_spikes>0)

# has_defensive_buff is 1 if demon form is active.
actions+=/variable,name=has_defensive_buff,value=1,value_else=0,op=setif,condition=buff.metamorphosis.up|buff.chromebustible_bomb_suit.up

# has_absorb_buff is 1 if an absorb shield buff is active.
actions+=/variable,name=has_absorb_buff,value=1,value_else=0,op=setif,condition=buff.soul_barrier.up|buff.chromebustible_bomb_suit.up|buff.mudborne.up

################

actions+=/disrupt
actions+=/consume_magic
actions+=/run_action_list,name=fs,if=hero_tree.felscarred
actions+=/run_action_list,name=ar

actions.cooldowns+=/use_item,name=tome_of_lights_devotion,use_off_gcd=1,if=buff.inner_resilience.up&buff.resilience_verses.stack_pct<50
actions.cooldowns+=/potion,if=buff.metamorphosis.remains>7
actions.cooldowns+=/use_items,if=buff.metamorphosis.remains>7

actions.defensives+=/use_item,name=ringing_ritual_mud,use_off_gcd=1,if=incoming_damage_5s>0&variable.has_defensive_buff=0&variable.has_absorb_buff=0
actions.defensives+=/use_item,name=chromebustible_bomb_suit,use_off_gcd=1,if=incoming_damage_5s>=variable.metamorphosis_damage_taken&variable.has_defensive_buff=0&variable.has_absorb_buff=0
actions.defensives+=/fel_devastation,if=!talent.demonsurge&variable.fel_devastation_ready>0&variable.has_fiery_brand_safety=0
actions.defensives+=/metamorphosis,if=!talent.demonsurge&variable.metamorphosis_ready>0
actions.defensives+=/darkness,if=incoming_damage_5s>=variable.darkness_damage_taken&variable.has_defensive_buff=0&(debuff.fiery_brand_dot.down&variable.has_fiery_brand_safety=0)&variable.has_illuminated_sigils_safety=0

actions.heal+=/bulk_extraction,if=(health.pct<variable.bulk_extraction_health_pct_threshold)&(variable.souls_deficit<=variable.num_targets)&(soul_fragments.active<=2)

# [1450143] :: Demon Spikes can be used but Fel Devastation is off cooldown.
actions.mitigation+=/demon_spikes,use_off_gcd=1,if=variable.can_demon_spikes>0&variable.fel_devastation_ready>0
# [1247262] :: Demon Spikes can be used but Metamorphosis is off cooldown.
actions.mitigation+=/demon_spikes,use_off_gcd=1,if=variable.can_demon_spikes>0&variable.fel_devastation_ready=0&variable.metamorphosis_ready>0
actions.mitigation+=/demon_spikes,use_off_gcd=1,if=variable.can_demon_spikes>0&variable.fel_devastation_ready=0&variable.metamorphosis_ready=0

#actions.racials+=/arcane_torrent

### Aldrachi Reaver ###

actions.ar_opener+=/demon_spikes,use_off_gcd=1,if=buff.demon_spikes.remains<gcd
actions.ar_opener+=/felblade
actions.ar_opener+=/immolation_aura
actions.ar_opener+=/infernal_strike
actions.ar_opener+=/sigil_of_spite
actions.ar_opener+=/fiery_brand,if=talent.burning_alive&active_dot.fiery_brand_dot=0
actions.ar_opener+=/fel_devastation

# ar_opener_duration is the approximate length of the Aldrachi Reaver opener used on pull.
actions.ar+=/variable,name=ar_opener_duration,value=5*gcd.max

actions.ar+=/call_action_list,name=ar_opener,if=time<variable.ar_opener_duration
actions.ar+=/call_action_list,name=mitigation,if=tanking
actions.ar+=/call_action_list,name=defensives,if=tanking
actions.ar+=/call_action_list,name=heal
actions.ar+=/call_action_list,name=cooldowns
actions.ar+=/call_action_list,name=ar_glaive
actions.ar+=/the_hunt,if=buff.reavers_glaive.down
actions.ar+=/infernal_strike,use_off_gcd=1,if=full_recharge_time<gcd
actions.ar+=/sigil_of_flame,if=!action.sigil_of_flame.placed&(talent.illuminated_sigils&(buff.metamorphosis.remains<variable.sigil_activation_duration+action.sigil_of_flame.execution_time)&variable.has_illuminated_sigils_safety=0)
actions.ar+=/fiery_brand,if=buff.metamorphosis.down&active_dot.fiery_brand_dot=0&variable.has_illuminated_sigils_safety=0
actions.ar+=/soul_carver,if=variable.num_targets=1&debuff.fiery_brand_dot.up
actions.ar+=/soul_carver,cycle_targets=1,if=variable.num_targets>1&debuff.fiery_brand_dot.up
actions.ar+=/sigil_of_spite
actions.ar+=/sigil_of_flame,if=!action.sigil_of_flame.placed&(!talent.illuminated_sigils&full_recharge_time<gcd)&variable.fury_gain_sigil_of_flame<=fury.deficit
actions.ar+=/immolation_aura
actions.ar+=/call_action_list,name=ar_spirit_bomb,strict=1,if=talent.spirit_bomb
# [1344653] :: Felblade to generate Fury to dump Souls with Soul Cleave.
actions.ar+=/felblade,if=!talent.spirit_bomb&variable.souls_deficit<=1&fury<variable.fury_cost_soul_cleave
# souls :: Dump Souls with Soul Cleave.
actions.ar+=/soul_cleave,if=!talent.spirit_bomb&variable.should_dump_souls>0
# fury :: Dump Fury with Soul Cleave.
actions.ar+=/soul_cleave,if=!talent.spirit_bomb&variable.should_dump_fury>0
actions.ar+=/fracture
actions.ar+=/felblade
actions.ar+=/shear,if=variable.souls_deficit>0
actions.ar+=/throw_glaive

actions.ar_glaive+=/reavers_glaive,if=(buff.glaive_flurry.down&buff.rending_strike.down)&(buff.thrill_of_the_fight_damage.down|buff.thrill_of_the_fight_damage.remains<4)
# [5927616] :: Empowered Soul Cleave from Art of the Glaive (single-target)
actions.ar_glaive+=/soul_cleave,if=buff.glaive_flurry.up&(variable.num_targets=1&(debuff.reavers_mark.down|debuff.reavers_mark.stack<2|debuff.reavers_mark.remains<gcd*2))
# [5927616] :: Empowered Fracture from Art of the Glaive
actions.ar_glaive+=/fracture,if=buff.rending_strike.up
# [5927616] :: Empowered Shear from Art of the Glaive
actions.ar_glaive+=/shear,if=buff.rending_strike.up
# [5927616] :: Empowered Soul Cleave from Art of the Glaive
actions.ar_glaive+=/soul_cleave,if=buff.glaive_flurry.up&(variable.num_targets>1|(debuff.reavers_mark.up&debuff.reavers_mark.remains>8))

# [1097742] :: Felblade to generate Fury to dump Souls with Spirit Bomb.
actions.ar_spirit_bomb+=/felblade,if=(variable.num_targets>1&variable.souls_deficit<=1|variable.souls_deficit=0)&fury<variable.fury_cost_spirit_bomb
# souls :: Dump Souls with Spirit Bomb.
actions.ar_spirit_bomb+=/spirit_bomb,if=variable.should_dump_souls>0
# [1097742] :: Fracture to generate Souls for Spirit Bomb.
actions.ar_spirit_bomb+=/fracture,if=variable.num_targets>1&(variable.souls_gain_fracture>=variable.souls_deficit-1)|(variable.souls_gain_fracture=variable.souls_deficit)
# [1097742] :: Dump Fury with Soul Cleave if it won't delay Spirit Bomb.
actions.ar_spirit_bomb+=/soul_cleave,if=soul_fragments.active=0&variable.should_dump_fury>0

### Fel-Scarred ###

actions.fs_opener+=/infernal_strike
actions.fs_opener+=/fiery_brand,if=talent.fiery_demise&active_dot.fiery_brand_dot=0
actions.fs_opener+=/demon_spikes,use_off_gcd=1,if=buff.demon_spikes.remains<gcd
actions.fs_opener+=/immolation_aura
actions.fs_opener+=/felblade
actions.fs_opener+=/fracture,if=!prev_gcd.fracture
actions.fs_opener+=/shear,if=!prev_gcd.shear

# fs_opener_duration is the approximate length of the Fel-Scarred opener used on pull.
actions.fs+=/variable,name=fs_opener_duration,value=4*gcd.max

# prep_time_fel_devastation is the amount of time needed to cast all spells before Fel Devastation.
actions.fs+=/variable,name=prep_time_fel_devastation,op=reset,default=0
actions.fs+=/variable,name=prep_time_fel_devastation,op=add,value=action.fiery_brand.execute_time,if=talent.fiery_demise&talent.down_in_flames&cooldown.fiery_brand.full_recharge_time<gcd

# prep_time_metamorphosis is the amount of time needed to cast all spells before Metamorphosis.
actions.fs+=/variable,name=prep_time_metamorphosis,op=reset,default=0
actions.fs+=/variable,name=prep_time_metamorphosis,op=add,value=variable.prep_time_fel_devastation
actions.fs+=/variable,name=prep_time_metamorphosis,op=add,value=action.sigil_of_flame.execute_time*cooldown.sigil_of_flame.charges,if=talent.violent_transformation

actions.fs+=/call_action_list,name=fs_opener,if=buff.metamorphosis.down&time<variable.fs_opener_duration
actions.fs+=/call_action_list,name=mitigation,if=tanking
actions.fs+=/call_action_list,name=defensives,if=tanking
actions.fs+=/call_action_list,name=heal
actions.fs+=/call_action_list,name=cooldowns
actions.fs+=/call_action_list,name=demonsurge,if=buff.metamorphosis.up&buff.metamorphosis.remains<gcd*(variable.demonsurge_remaining+1)
actions.fs+=/run_action_list,name=metamorphosis,if=(cooldown.metamorphosis.remains<=variable.prep_time_metamorphosis)&(!cooldown.fel_devastation.ready&cooldown.fel_devastation.remains>10)&variable.demonsurge_remaining=0
# [1450143] :: Felblade to generate Fury for Fel Devastation.
actions.fs+=/felblade,if=cooldown.fel_devastation.ready&fury<variable.fury_cost_fel_devastation
actions.fs+=/call_action_list,name=fel_devastation,if=(!talent.demonic|time<(variable.fs_opener_duration+variable.prep_time_fel_devastation+action.fel_devastation.execute_time)|buff.demon_spikes.remains<gcd)&(cooldown.fel_devastation.remains<=variable.prep_time_fel_devastation)
actions.fs+=/the_hunt
actions.fs+=/infernal_strike,use_off_gcd=1,if=full_recharge_time<gcd
actions.fs+=/fiery_brand,if=!talent.fiery_demise&active_dot.fiery_brand_dot=0
actions.fs+=/sigil_of_spite
actions.fs+=/soul_carver
# [5927628] :: Activate Demonsurge with Fel Desolation.
actions.fs+=/fel_desolation,if=buff.demonsurge_fel_desolation.up
# [5927628] :: Activate Demonsurge with Sigil of Doom.
actions.fs+=/sigil_of_doom,if=!(action.sigil_of_doom.placed|action.sigil_of_flame.placed)&buff.demonsurge_sigil_of_doom.up&full_recharge_time<gcd
actions.fs+=/sigil_of_flame,if=!(action.sigil_of_doom.placed|action.sigil_of_flame.placed)&((talent.illuminated_sigils&buff.metamorphosis.down&variable.has_illuminated_sigils_safety=0)|(!talent.illuminated_sigils&full_recharge_time<gcd))
actions.fs+=/run_action_list,name=fs_aoe,if=variable.num_targets>1
actions.fs+=/run_action_list,name=fs_st

# [5927628] :: Activate Demonsurge with Spirit Burst to dump Souls.
actions.demonsurge+=/spirit_burst,if=buff.demonsurge_spirit_burst.up&variable.should_dump_souls>0
# [5927628] :: Activate Demonsurge with Soul Sunder.
actions.demonsurge+=/soul_sunder,if=buff.demonsurge_soul_sunder.up&variable.should_dump_fury>0
# [5927628] :: Activate Demonsurge with Consuming Fire.
actions.demonsurge+=/consuming_fire,if=buff.demonsurge_consuming_fire.up
# [5927628] :: Activate Demonsurge with Spirit Burst.
actions.demonsurge+=/spirit_burst,if=buff.demonsurge_spirit_burst.up&soul_fragments.active>0
# [5927628] :: Activate Demonsurge with Soul Sunder.
actions.demonsurge+=/soul_sunder,if=buff.demonsurge_soul_sunder.up
# [5927628] :: Activate Demonsurge with Sigil of Doom.
actions.demonsurge+=/sigil_of_doom,if=buff.demonsurge_sigil_of_doom.up

# [1450143] :: Fiery Brand in preparation for Fel Devastation.
actions.fel_devastation+=/fiery_brand,if=talent.fiery_demise&talent.down_in_flames&full_recharge_time<gcd
actions.fel_devastation+=/fel_devastation

# [1247263] :: Fiery Brand in preparation for Metamorphosis.
actions.metamorphosis+=/fiery_brand,if=talent.fiery_demise&talent.down_in_flames&full_recharge_time<gcd
# [1247263] :: Sigil of Flame in preparation for Metamorphosis.
actions.metamorphosis+=/sigil_of_flame
actions.metamorphosis+=/metamorphosis,if=cooldown.sigil_of_flame.charges<1

actions.fs_aoe+=/spirit_burst,if=variable.should_dump_souls>0
actions.fs_aoe+=/spirit_bomb,if=variable.should_dump_souls>0
actions.fs_aoe+=/consuming_fire
actions.fs_aoe+=/immolation_aura
actions.fs_aoe+=/fracture,if=variable.souls_deficit<=1+variable.souls_gain_fracture&active_dot.fiery_brand_dot>0
# [1097742] :: Dump Fury with Soul Sunder if it won't delay Spirit Burst.
actions.fs_aoe+=/soul_sunder,if=soul_fragments.active=0&variable.should_dump_fury>0
# [1097742] :: Dump Fury with Soul Cleave if it won't delay Spirit Bomb.
actions.fs_aoe+=/soul_cleave,if=soul_fragments.active=0&variable.should_dump_fury>0
actions.fs_aoe+=/felblade,if=variable.fury_gain_felblade<=fury.deficit
actions.fs_aoe+=/fracture,if=variable.fury_gain_fracture<=fury.deficit|!prev_gcd.fracture
actions.fs_aoe+=/shear,if=variable.souls_deficit>0&variable.fury_gain_shear<=fury.deficit
# [5927628] :: Activate Demonsurge with Spirit Burst to dump Fury.
actions.fs_aoe+=/spirit_burst,if=buff.demonsurge_spirit_burst.up&soul_fragments.active>0&variable.should_dump_fury>0
# fury :: Dump Fury with Soul Sunder.
actions.fs_aoe+=/soul_sunder,if=variable.should_dump_fury>0
# fury :: Dump Fury with Soul Cleave.
actions.fs_aoe+=/soul_cleave,if=variable.should_dump_fury>0
actions.fs_aoe+=/throw_glaive

actions.fs_st+=/spirit_burst,if=talent.fiery_demise&debuff.fiery_brand_dot.up&variable.should_dump_souls>0
actions.fs_st+=/spirit_bomb,if=talent.fiery_demise&debuff.fiery_brand_dot.up&variable.should_dump_souls>0
actions.fs_st+=/consuming_fire
actions.fs_st+=/immolation_aura
# [5927628] :: Activate Demonsurge with Spirit Burst to dump Fury.
actions.fs_st+=/spirit_burst,if=buff.demonsurge_spirit_burst.up&soul_fragments.active>0&variable.should_dump_fury>0
# fury :: Dump Fury with Soul Sunder.
actions.fs_st+=/soul_sunder,if=variable.should_dump_fury>0
# fury :: Dump Fury with Soul Cleave.
actions.fs_st+=/soul_cleave,if=variable.should_dump_fury>0
actions.fs_st+=/felblade
# [5927628] :: Activate Demonsurge with Spirit Burst to dump Souls.
actions.fs_st+=/spirit_burst,if=buff.demonsurge_spirit_burst.up&variable.should_dump_souls>0
# souls :: Dump Souls with Soul Sunder.
actions.fs_st+=/soul_sunder,if=variable.should_dump_souls>0
# souls :: Dump Souls with Soul Cleave.
actions.fs_st+=/soul_cleave,if=variable.should_dump_souls>0
actions.fs_st+=/fracture
actions.fs_st+=/shear,if=variable.souls_deficit>0
actions.fs_st+=/throw_glaive
