actions.precombat+=/sigil_of_flame,if=variable.sigil_of_flame_placed=0
actions.precombat+=/metamorphosis,if=buff.metamorphosis.down
actions.precombat+=/demon_spikes,use_off_gcd=1,if=buff.demon_spikes.down&buff.metamorphosis.down
actions.precombat+=/use_item,name=fyrakks_tainted_rageheart,if=buff.demon_spikes.down&buff.metamorphosis.down
actions.precombat+=/infernal_strike,use_off_gcd=1
actions.precombat+=/fiery_brand,if=active_dot.fiery_brand_dot=0
actions.precombat+=/the_hunt

actions+=/variable,name=fury_cost_fel_devastation,value=50
actions+=/variable,name=fury_cost_soul_cleave,value=30
actions+=/variable,name=fury_cost_spirit_bomb,value=40
actions+=/variable,name=fury_gain_felblade,value=40
actions+=/variable,name=fury_gain_fracture,value=45,op=setif,condition=buff.metamorphosis.up,value_else=25
actions+=/variable,name=fury_gain_shear_base,value=10+(10*talent.shear_fury.enabled)
actions+=/variable,name=fury_gain_shear,value=variable.fury_gain_shear_base+20,op=setif,condition=buff.metamorphosis.up,value_else=variable.fury_gain_shear_base
actions+=/variable,name=fury_gain_sigil_of_flame,value=30

actions+=/variable,name=bulk_extraction_health_pct_threshold,value=70
actions+=/variable,name=soul_cleave_health_pct_threshold,value=70

actions+=/variable,name=fiery_demise_pool_amount,value=100
# Pool enough Fury so that the Fury bar is not empty when exiting Fel Devastation.
actions+=/variable,name=fel_devastation_pool_amount,value=variable.fury_cost_fel_devastation,op=setif,condition=talent.darkglare_boon.enabled,value_else=variable.fury_cost_fel_devastation+variable.fury_cost_soul_cleave

actions+=/variable,name=immolation_aura_crit,value=stat.crit+0.06,op=setif,condition=talent.aura_of_pain.enabled,value_else=stat.crit

actions+=/variable,name=fury_per_second_felblade,value=variable.fury_gain_felblade%cooldown.felblade.duration,op=setif,condition=talent.felblade.enabled,value_else=0
actions+=/variable,name=fury_per_second_fracture,value=variable.fury_gain_fracture%(4.5*gcd.max),op=setif,condition=talent.fracture.enabled,value_else=0
actions+=/variable,name=fury_per_second_volatile_flameblood,value=7.5*variable.immolation_aura_crit,op=setif,condition=talent.volatile_flamebood.enabled,value_else=0
actions+=/variable,name=fury_per_second_immolation_aura,value=26%cooldown.immolation_aura.duration+variable.fury_per_second_volatile_flameblood
actions+=/variable,name=fury_per_second_shear,value=variable.fury_gain_shear%gcd.max,op=setif,condition=!talent.fracture.enabled,value_else=0
actions+=/variable,name=fury_per_second,value=variable.fury_per_second_felblade+variable.fury_per_second_fracture+variable.fury_per_second_immolation_aura+variable.fury_per_second_shear
actions+=/variable,name=fury_at_next_fel_devastation,value=fury+cooldown.fel_devastation.remains*variable.fury_per_second
actions+=/variable,name=fury_at_next_fiery_brand,value=fury+cooldown.fiery_brand.remains*variable.fury_per_second
# Pool Fury for Fel Devastation and Fiery Demise windows.
actions+=/variable,name=should_pool_fury,value=0,op=setif,condition=(variable.fury_at_next_fel_devastation>=variable.fel_devastation_pool_amount)&(!talent.fiery_demise.enabled|variable.fury_at_next_fiery_brand>=variable.fiery_demise_pool_amount),value_else=1

actions+=/variable,name=soul_fragments_max,value=5
actions+=/variable,name=soul_fragments_deficit,value=variable.soul_fragments_max-soul_fragments
actions+=/variable,name=soul_fragments_gain_fracture,value=3,op=setif,condition=buff.metamorphosis.up,value_else=2
actions+=/variable,name=soul_fragments_gain_immolation_aura,value=0.7*spell_targets.immolation_aura,op=setif,condition=talent.fallout.enabled,value_else=0
actions+=/variable,name=soul_fragments_gain_shear,value=2,op=setif,condition=buff.metamorphosis.up,value_else=1
actions+=/variable,name=soul_fragments_gain_soul_carver,value=3

actions+=/variable,name=shattered_souls_heal_amount_base,value=(0.06*incoming_damage_5s)<?(0.01*health.max)
actions+=/variable,name=shattered_souls_heal_amount,value=variable.shattered_souls_healing_amount_base*0.1,op=setif,condition=talent.shattered_restoration.enabled,value_else=variable.shattered_souls_healing_amount_base
actions+=/variable,name=spirit_bomb_heal_amount,value=variable.shattered_souls_heal_amount*soul_fragments
actions+=/variable,name=soul_cleave_heal_amount_base,value=0.5*stat.attack_power
actions+=/variable,name=soul_cleave_heal_amount,value=variable.soul_cleave_heal_amount_base+((variable.soul_cleave_heal_amount_base+variable.shattered_souls_heal_amount)*(soul_fragments<?2))

# Check whether you are in a multi-target situation.
actions+=/variable,name=num_targets,value=active_enemies,op=setif,condition=!talent.spirit_bomb.enabled,value_else=spell_targets.spirit_bomb
actions+=/variable,name=aoe,value=1,op=setif,condition=variable.num_targets>1,value_else=0

# Check if a sigil has already been placed and waiting for activation.
actions+=/variable,name=sigil_of_spite_placed,value=1,op=setif,condition=prev_gcd.sigil_of_spite&(talent.quickened_sigils.enabled|prev_gcd.2.sigil_of_spite),value_else=0
actions+=/variable,name=sigil_of_flame_placed,value=1,op=setif,condition=prev_gcd.sigil_of_flame&(talent.quickened_sigils.enabled|prev_gcd.2.sigil_of_flame),value_else=0

# Try to synchronize Fiery Brand with major fire spells if Fiery Demise is talented.
actions+=/variable,name=fiery_demise_ready,value=1,op=setif,condition=cooldown.sigil_of_flame.remains<=(execute_time+gcd.remains)|(talent.soul_carver.enabled&cooldown.soul_carver.remains<=(execute_time+gcd.remains))|cooldown.fel_devastation.remains<=(execute_time+gcd.remains),value_else=0

# Try to synchronize major offensive cooldowns with Frailty on targets.
actions+=/variable,name=frailty_ready,value=1,op=setif,condition=(talent.the_hunt.enabled&cooldown.the_hunt.remains<=(execute_time+gcd.remains))|(talent.sigil_of_spite.enabled&cooldown.sigil_of_spite.remains<=(execute_time+gcd.remains))|(talent.soul_carver.enabled&cooldown.soul_carver.remains<=(execute_time+gcd.remains)),value_else=0
actions+=/variable,name=frail_target,value=1,op=setif,condition=!talent.vulnerability.enabled|(variable.aoe=0&(talent.soulcrush.enabled&debuff.frailty.stack>=6)|active_dot.frailty>0),value_else=0
actions+=/variable,name=frail_multitarget,value=1,op=setif,condition=!talent.vulnerability.enabled|(variable.aoe=1&(!talent.spirit_bomb_enabled&active_dot.frailty>1)|active_dot.frailty>=variable.num_targets),value_else=0

actions+=/disrupt
actions+=/consume_magic
actions+=/call_action_list,name=heal
actions+=/call_action_list,name=mitigation,if=tanking&buff.metamorphosis.down
actions+=/run_action_list,name=multitarget,if=variable.aoe=1
actions+=/call_action_list,name=priority

actions.heal+=/bulk_extraction,if=talent.bulk_extraction.enabled&(health.pct<variable.bulk_extraction_health_pct_threshold)&(variable.soul_fragments_deficit<=variable.num_targets)&(soul_fragments<=2)
actions.heal+=/soul_cleave,if=(health.pct<variable.soul_cleave_health_pct_threshold)&(incoming_damage_5s>(health.max*0.2))

actions.mitigation+=/demon_spikes,use_off_gcd=1,if=buff.demon_spikes.down&cooldown.fel_devastation.remains>(execute_time+gcd.remains)
actions.mitigation+=/soul_barrier,if=soul_fragments>=4&buff.demon_spikes.down
actions.mitigation+=/use_item,name=fyrakks_tainted_rageheart,use_off_gcd=1,if=buff.demon_spikes.down
actions.mitigation+=/metamorphosis,use_off_gcd=1,if=!talent.demonic.enabled|cooldown.fel_devastation.remains>12

actions.racials=blood_fury
actions.racials+=/berserking
#actions.racials+=/arcane_torrent
actions.racials+=/lights_judgment
actions.racials+=/ancestral_call
#actions.racials+=/fireblood
actions.racials+=/bag_of_tricks

actions.multitarget+=/infernal_strike,use_off_gcd=1,if=full_recharge_time<gcd
actions.multitarget+=/spirit_bomb,if=time<(execute_time+gcd.remains)&talent.frailty.enabled&variable.frailty_ready=1&active_dot.frailty<spell_targets
actions.multitarget+=/fel_devastation,if=!talent.fiery_demise.enabled
actions.multitarget+=/fiery_brand,if=(!talent.fiery_demise.enabled|variable.fiery_demise_ready=1|fury>=variable.fiery_demise_pool_amount)&active_dot.fiery_brand_dot=0
actions.multitarget+=/sigil_of_flame,if=variable.sigil_of_flame_placed=0&(!talent.illuminated_sigils.enabled|(active_dot.sigil_of_flame=0&fury.deficit>=variable.fury_gain_sigil_of_flame))
actions.multitarget+=/fel_devastation,if=talent.fiery_demise.enabled
actions.multitarget+=/the_hunt,if=variable.frail_multitarget=1
actions.multitarget+=/sigil_of_spite,if=variable.sigil_of_spite_placed=0&variable.frail_multitarget=1
actions.multitarget+=/soul_carver,if=(variable.soul_fragments_deficit>=variable.soul_fragments_gain_soul_carver)&variable.frail_multitarget=1
actions.multitarget+=/spirit_bomb,if=(buff.metamorphosis.up&variable.soul_fragments_deficit<=2)|variable.soul_fragments_deficit<=1
actions.multitarget+=/soul_cleave,if=variable.should_pool_fury=0
actions.multitarget+=/immolation_aura
actions.multitarget+=/fracture,if=variable.soul_fragments_deficit>=variable.soul_fragments_gain_fracture&fury.deficit>=variable.fury_gain_fracture
actions.multitarget+=/felblade,if=fury.deficit>variable.fury_gain_felblade
actions.multitarget+=/shear,if=variable.soul_fragments_deficit>=variable.soul_fragments_gain_shear&fury.deficit>=variable.fury_gain_shear
actions.multitarget+=/throw_glaive

actions.priority+=/infernal_strike,use_off_gcd=1,if=full_recharge_time<gcd
actions.priority+=/spirit_bomb,if=time<(execute_time+gcd.remains)&talent.frailty.enabled&variable.frailty_ready=1&active_dot.frailty<spell_targets
actions.priority+=/fel_devastation,if=!talent.fiery_demise.enabled
actions.priority+=/fiery_brand,if=(!talent.fiery_demise.enabled|variable.fiery_demise_ready=1|fury>=variable.fiery_demise_pool_amount)&active_dot.fiery_brand_dot=0
actions.priority+=/sigil_of_flame,if=variable.sigil_of_flame_placed=0&(!talent.illuminated_sigils.enabled|(active_dot.sigil_of_flame=0&fury.deficit>=variable.fury_gain_sigil_of_flame))
actions.priority+=/fel_devastation,if=talent.fiery_demise.enabled
actions.priority+=/the_hunt,if=variable.frail_target=1
actions.priority+=/sigil_of_spite,if=variable.sigil_of_spite_placed=0&variable.frail_target=1
actions.priority+=/soul_carver,if=(variable.soul_fragments_deficit>=variable.soul_fragments_gain_soul_carver)&variable.frail_target=1&debuff.fiery_brand_dot.remains>3
actions.priority+=/spirit_bomb,if=talent.fiery_demise.enabled&debuff.fiery_brand_dot.up&variable.soul_fragments_deficit=0
actions.priority+=/soul_cleave,if=variable.should_pool_fury=0
actions.priority+=/immolation_aura
actions.priority+=/felblade,if=fury.deficit>variable.fury_gain_felblade
actions.priority+=/fracture,if=variable.soul_fragments_deficit>=variable.soul_fragments_gain_fracture&fury.deficit>=variable.fury_gain_fracture
actions.priority+=/shear,if=variable.soul_fragments_deficit>=variable.soul_fragments_gain_shear&fury.deficit>=variable.fury_gain_shear
actions.priority+=/throw_glaive
