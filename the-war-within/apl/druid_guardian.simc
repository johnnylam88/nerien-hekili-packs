### Guardian Druid Action Priority List
### Based on *Guardian Druid Tank Guide* by Pumps
### [https://www.icy-veins.com/wow/guardian-druid-pve-tank-guide]
###
### Recommended Settings for `/hekili` > *Guardian*:
###
### - Weave Cat Form and Bear Form : enabled
### - Maul (or Raze) Rage Threshold : 0
### - Nature's Vigil Damage Threshold : 70
### - Ironfur Damage Threshold : 5
### - Ironfur Maximum Stacks : 14

### Tunables ###

# Set a soft cap for maximum Rage.
# This value is used when checking if we'll cap on Rage.
# It's less than maximum Rage because our Rage generation estimates tend to
# underestimate the true Rage generation due to procs, and comparing against
# the true max Rage has a higher probability of capping.
actions+=/variable,name=rage_cap,value=(rage.max-10)
# Always maintain enough Rage for Ironfur or Frenzied Regeneration.
actions+=/variable,name=rage_floor,value=(variable.rage_cost_ironfur+variable.rage_cost_frenzied_regeneration)
# Pool enough Rage to cast Ironfur twice.
actions+=/variable,name=rage_pool,value=(variable.rage_cap-20)
# Barkskin tunable for incoming damage.
actions+=/variable,name=barkskin_damage_taken,value=0.2*health.max
# Frenzied Regeneration tunable for minimum health percent.
actions+=/variable,name=frenzied_regeneration_health_pct,value=50
# Ironfur tunable for incoming damage per suggested application (see /hekili > Guardian).
actions+=/variable,name=ironfur_damage_taken,value=ironfur_damage_threshold
# Nature's Vigil tunable for minimum health percent (see /hekili > Guardian).
actions+=/variable,name=natures_vigil_health_pct,value=vigil_damage
# Renewal tunable for minimum health percent.
actions+=/variable,name=renewal_health_pct,value=65
# Regrowth tunable for minimum health percent.
actions+=/variable,name=regrowth_health_pct,value=85
# Survival Instincts tunable for incoming damage.
actions+=/variable,name=survival_instincts_damage_taken,value=0.3*health.max

# The maximum number of defensive buffs that can be recommended to be layered (minimum value is 1).
actions+=/variable,name=defensive_layer_max_stack,value=3
# The incoming damage threshold before allowing a defensive layer of stack size 1.
actions+=/variable,name=defensive_layer_threshold_1,value=(0.2*health.max)
# The incoming damage threshold before allowing a defensive layer of stack size 2.
actions+=/variable,name=defensive_layer_threshold_2,value=(0.4*health.max)
# The incoming damage threshold before allowing a defensive layer of stack size 3.
actions+=/variable,name=defensive_layer_threshold_3,value=(0.6*health.max)

### Constants ###

actions+=/variable,name=rage_cost_frenzied_regeneration,value=10
actions+=/variable,name=rage_cost_ironfur,value=40
actions+=/variable,name=rage_cost_maul,value=40
actions+=/variable,name=rage_cost_raze,value=40

actions+=/variable,name=rage_gain_mangle,value=(12+5*talent.soul_of_the_forest+4*buff.gore.up)
actions+=/variable,name=rage_gain_thrash_bear,value=5

################

# ripweave is 1 if the conditions for Ripweaving are true (Rip, Rake, Fluid Form, and Wildpower Surge are talented).
actions+=/variable,name=ripweave,value=1,value_else=0,op=setif,condition=druid.catweave_bear&talent.rip&talent.rake&talent.fluid_form&talent.wildpower_surge

# has_thrash_bear_threat is 1 if the percentage of enemy mobs with the Thrash DoT on them exceeds 75%.
actions+=/variable,name=has_thrash_bear_threat,value=1,value_else=0,op=setif,condition=(active_dot.thrash_bear%active_enemies*100)>75

# Amount of Rage until the Rage cap is reached.
actions+=/variable,name=rage_deficit,value=(variable.rage_cap-rage)

# 4 Rage per auto-attack.
actions+=/variable,name=rage_per_second_auto_attack,value=4%(2*attack_haste)
# 3 Rage per incoming auto-attack, and incoming auto-attack every 2s with 1 mob.
# Assume at 1 mob, we don't get any rage, but at 5+ mobs that we gain 3 Rage per second.
actions+=/variable,name=rage_per_second_incoming_auto_attack,value=(3%4)*(active_enemies-1),value_else=3,op=setif,condition=active_enemies<5
actions+=/variable,name=rage_per_second_incoming_auto_attack,value=(3%2)+((3%2)%(5-1))*(active_enemies-1),value_else=3,op=setif,condition=active_enemies<5
# Blood Frenzy causes the Thrash DoT to generate 3 Rage per tick on up to 5 targets.
actions+=/variable,name=rage_per_second_blood_frenzy,value=talent.blood_frenzy*(3%dot.thrash_bear.tick_time)*(active_dot.thrash_bear>?5)
# Galactic Guardian causes damage to have a 5% chance to trigger a free Moonfire on that target, and the next Moonfire cast generates 8 Rage.
# This underestimates the Rage generated because we only factor in Thrash ticks to proc Galactic Guardian.
actions+=/variable,name=rage_per_second_galactic_guardian,value=talent.galactic_guardian*8*0.05*(active_dot.thrash_bear%dot.thrash_bear.tick_time)
# Moon Guardian causes the free Moonfires from Galactic Guardian to generate 5 Rage.
# Galactic Guardian causes damage to have a 5% chance to trigger a free Moonfire on that target.
# This underestimates the Rage generated because we only factor in Thrash ticks to proc Galactic Guardian.
actions+=/variable,name=rage_per_second_moon_guardian,value=talent.moon_guardian*talent.galactic_guardian*5*0.05*(active_dot.thrash_bear%dot.thrash_bear.tick_time)
actions+=/variable,name=rage_per_second,value=variable.rage_per_second_auto_attack+variable.rage_per_second_incoming_auto_attack+variable.rage_per_second_blood_frenzy+variable.rage_per_second_galactic_guardian+variable.rage_per_second_moon_guardian
actions+=/variable,name=rage_at_next_gcd,value=rage+gcd.remains*variable.rage_per_second
# should_spend_rage is 1 if the expected Rage by the next GCD exceeds the pool value.
actions+=/variable,name=should_spend_rage,value=1,value_else=0,op=setif,condition=(variable.rage_at_next_gcd>variable.rage_pool)
# can_spend_rage_on_maul is 1 if casting Maul doesn't drop Rage below the floor value.
actions+=/variable,name=can_spend_rage_on_maul,value=1,value_else=0,op=setif,condition=(rage-variable.rage_cost_maul>=variable.rage_floor)
# can_spend_rage_on_raze is 1 if casting Raze doesn't drop Rage below the floor value.
actions+=/variable,name=can_spend_rage_on_raze,value=1,value_else=0,op=setif,condition=(rage-variable.rage_cost_raze>=variable.rage_floor)

# time_to_rage_cap is the number of seconds before a Rage-generating ability comes off cooldown and will cap total Rage if cast.
actions+=/variable,name=time_to_rage_cap,op=reset,default=(variable.rage_deficit%variable.rage_per_second)
actions+=/variable,name=time_to_rage_cap,op=min,value=cooldown.mangle.remains,if=((cooldown.mangle.remains*variable.rage_per_second+variable.rage_gain_mangle)>=variable.rage_deficit)
actions+=/variable,name=time_to_rage_cap,op=min,value=cooldown.thrash_bear.remains,if=talent.thrash&((cooldown.thrash_bear.remains*variable.rage_per_second+variable.rage_gain_thrash_bear)>=variable.rage_deficit)
# Moonfire with Galactic Guardian generates 8 Rage.
actions+=/variable,name=time_to_rage_cap,op=min,value=gcd.remains,if=talent.galactic_guardian&buff.galactic_guardian.up&((gcd.remains*variable.rage_per_second+8)>=variable.rage_deficit)

# Ironfur if not already up, about to expire, or we need more stacks.
actions+=/variable,name=ironfur_threshold,value=floor(incoming_physical_damage_5s%variable.ironfur_damage_taken)
actions+=/variable,name=should_ironfur,value=1,value_else=0,op=setif,condition=(buff.ironfur.down|buff.ironfur.remains<2|buff.ironfur.stack<variable.ironfur_threshold)

# berserk_ready is 1 if Berserk or Incarnation is imminent.
actions+=/variable,name=berserk_ready,value=1,value_else=0,op=setif,condition=(talent.incarnation&cooldown.incarnation.remains<gcd)|(!talent.incarnation&cooldown.berserk_bear.remains<gcd)

# has_absorb_buff is 1 if there is an existing damage absorption buff.
actions+=/variable,name=has_absorb_buff,value=1,value_else=0,op=setif,condition=(buff.chromebustible_bomb_suit.up|buff.mudborne.up|buff.ethereal_barrier.up|buff.ethereal_barricade.up)

# defensive_layer_stack is the number of self-activiated damage reduction buffs that are active.
actions+=/variable,name=defensive_layer_stack,op=reset,default=0
actions+=/variable,name=defensive_layer_stack,op=add,value=1,if=talent.lunar_beam&buff.lunar_beam.up
actions+=/variable,name=defensive_layer_stack,op=add,value=1,if=buff.berserk_bear.up
actions+=/variable,name=defensive_layer_stack,op=add,value=1,if=talent.incarnation&buff.incarnation.up
actions+=/variable,name=defensive_layer_stack,op=add,value=1,if=talent.barkskin&buff.barkskin.up
actions+=/variable,name=defensive_layer_stack,op=add,value=1,if=talent.rage_of_the_sleeper&buff.rage_of_the_sleeper.up
actions+=/variable,name=defensive_layer_stack,op=add,value=1,if=buff.ethereal_barrier.up
actions+=/variable,name=defensive_layer_stack,op=add,value=1,if=buff.ethereal_barricade.up
actions+=/variable,name=defensive_layer_stack,op=add,value=1,if=talent.survival_instincts&buff.survival_instincts.up
actions+=/variable,name=defensive_layer_stack,op=add,value=1,if=buff.chromebustible_bomb_suit.up

# can_use_defensive_buff is 1 if we should layer another defensive buff based on incoming damage.
actions+=/variable,name=can_use_defensive_buff,op=reset,default=0
actions+=/variable,name=can_use_defensive_buff,op=set,value=1,if=(time=0|incoming_damage_5s>variable.defensive_layer_threshold_1)&variable.defensive_layer_stack<1
actions+=/variable,name=can_use_defensive_buff,op=set,value=1,if=(incoming_damage_5s>variable.defensive_layer_threshold_2)&variable.defensive_layer_stack<2
actions+=/variable,name=can_use_defensive_buff,op=set,value=1,if=(incoming_damage_5s>variable.defensive_layer_threshold_3)&variable.defensive_layer_stack<3
actions+=/variable,name=can_use_defensive_buff,op=set,value=0,if=variable.defensive_layer_stack>=variable.defensive_layer_max_stack

################

actions.on_pull+=/bear_form,if=buff.feline_potential.down&buff.feline_potential_counter.stack<6
actions.on_pull+=/mark_of_the_wild
# pull :: Enter pull with absorb shield from Chromebustible Bomb Suit.
actions.on_pull+=/use_item,name=chromebustible_bomb_suit,use_off_gcd=1,if=variable.can_use_defensive_buff>0&variable.has_absorb_buff=0
# pull :: Enter pull with absorb shield from Ringing Ritual Mud.
actions.on_pull+=/use_item,name=ringing_ritual_mud,use_off_gcd=1,if=variable.can_use_defensive_buff>0&variable.has_absorb_buff=0
# pull :: Enter pull with Barkskin.
actions.on_pull+=/barkskin,use_off_gcd=1,if=variable.can_use_defensive_buff>0&variable.has_absorb_buff=0

actions+=/call_action_list,name=on_pull,strict=1,if=time=0
actions+=/skull_bash
actions+=/soothe
# [132276] :: Mangle to switch back to Bear Form.
actions+=/mangle,if=buff.bear_form.down&talent.fluid_form&buff.feline_potential.down
actions+=/bear_form,if=!talent.fluid_form&buff.feline_potential.down
actions+=/call_action_list,name=mitigation
actions+=/call_action_list,name=defensives,if=tanking
actions+=/call_action_list,name=heal
actions+=/run_action_list,name=bear,if=buff.bear_form.up
actions+=/run_action_list,name=cat,if=buff.cat_form.up

actions.cooldowns+=/use_item,name=tome_of_lights_devotion,use_off_gcd=1,if=buff.inner_resilience.up&buff.resilience_verses.stack_pct<50
actions.cooldowns+=/potion,if=buff.berserk_bear.remains>8|buff.incarnation.remains>8
actions.cooldowns+=/use_items,if=buff.berserk_bear.remains>8|buff.incarnation.remains>8
actions.cooldowns+=/call_action_list,name=racials,if=buff.berserk_bear.remains>8|buff.incarnation.remains>8

actions.defensives+=/use_item,name=ringing_ritual_mud,use_off_gcd=1,if=incoming_damage_5s>0&(buff.berserk_bear.down&buff.incarnation.down)&variable.berserk_ready=0&variable.has_absorb_buff=0
actions.defensives+=/barkskin,use_off_gcd=1,if=incoming_damage_5s>=variable.barkskin_damage_taken&variable.can_use_defensive_buff>0&variable.has_absorb_buff=0
actions.defensives+=/use_item,name=chromebustible_bomb_suit,use_off_gcd=1,if=incoming_damage_5s>=variable.barkskin_damage_taken&(buff.berserk_bear.down&buff.incarnation.down)&variable.berserk_ready=0&variable.can_use_defensive_buff>0&variable.has_absorb_buff=0
actions.defensives+=/survival_instincts,use_off_gcd=1,if=incoming_damage_5s>=variable.survival_instincts_damage_taken&variable.can_use_defensive_buff>0&variable.has_absorb_buff=0

actions.heal+=/natures_vigil,if=health.pct<variable.natures_vigil_health_pct
actions.heal+=/regrowth,if=buff.dream_of_cenarius.up&health.pct<variable.regrowth_health_pct
actions.heal+=/frenzied_regeneration,if=buff.frenzied_regeneration.down&health.pct<variable.frenzied_regeneration_health_pct&charges_fractional>0.8
actions.heal+=/renewal,if=health.pct<variable.renewal_health_pct

actions.mitigation+=/ironfur,use_off_gcd=1,if=variable.should_ironfur>0

actions.racials+=/berserking

# cat_duration is the amount of time spent in Cat Form while Ripweaving.
actions+=/variable,name=cat_duration,op=reset,default=0
# Rake or Shred to enter Cat Form with Fluid Form.
actions+=/variable,name=cat_duration,op=add,value=action.rake.execute_time,if=talent.rake
# Heart of the Wild in Cat Form for damage.
actions+=/variable,name=cat_duration,op=add,value=action.heart_of_the_wild.execute_time,if=talent.heart_of_the_wild&cooldown.heart_of_the_wild.ready
# Convoke the Spirits in Cat Form for damage.
actions+=/variable,name=cat_duration,op=add,value=action.convoke_the_spirits.execute_time,if=talent.convoke_the_spirits&cooldown.convoke_the_spirits.ready
# Rip or Ferocious Bite to spend 5 Combo Points from Wildpower Surge and Feline Potential.
actions+=/variable,name=cat_duration,op=add,value=action.rip.execute_time,if=talent.rip

# [132115] :: Heart of the Wild to increase damage in Cat Form.
actions.cat+=/heart_of_the_wild
# [132115] :: Convoke the Spirits for damage in Cat Form.
actions.cat+=/convoke_the_spirits
# [132115] :: Apply or refresh Rip.
actions.cat+=/rip,if=combo_points=5&(!ticking|dot.rip.refreshable)
# [132115] :: Ferocious Bite
actions.cat+=/ferocious_bite,if=combo_points=5
# [132115] :: Apply or refresh Rake.
actions.cat+=/rake,if=!ticking|dot.rake.refreshable
# [132115] :: Shred for Combo Points.
actions.cat+=/shred

actions.bear+=/heart_of_the_wild,if=variable.ripweave=0
actions.bear+=/maul,if=buff.ravage.up
# stack :: Maintain max stacks of Thrash.
actions.bear+=/thrash_bear,if=!ticking|refreshable|!dot.thrash_bear.at_max_stacks|variable.has_thrash_bear_threat=0
actions.bear+=/moonfire,if=active_enemies<=2&(!ticking|refreshable)&target.time_to_die>12
actions.bear+=/call_action_list,name=ripweave,if=variable.ripweave>0
actions.bear+=/convoke_the_spirits,if=variable.ripweave=0
actions.bear+=/berserk_bear,if=buff.berserk_bear.down
actions.bear+=/incarnation,if=buff.incarnation.down
actions.bear+=/call_action_list,name=cooldowns
actions.bear+=/lunar_beam,if=variable.can_use_defensive_buff>0
actions.bear+=/rage_of_the_sleeper,if=variable.can_use_defensive_buff>0
actions.bear+=/call_action_list,name=ursocs_guidance,if=(talent.ursocs_guidance&buff.incarnation.up)
# [134298] :: Maul is free with Tooth and Claw proc.
actions.bear+=/maul,if=buff.tooth_and_claw.up
# [134298] :: Raze is free with Tooth and Claw proc.
actions.bear+=/raze,if=buff.tooth_and_claw.up
actions.bear+=/thrash_bear
actions.bear+=/mangle
actions.bear+=/call_action_list,name=dump_rage
actions.bear+=/call_action_list,name=spend_rage,if=variable.should_ironfur=0&variable.should_spend_rage>0
# [135853] :: Moonfire with Galactic Guardian
actions.bear+=/moonfire,if=buff.galactic_guardian.up&(buff.galactic_guardian.remains<action.moonfire.execute_time+gcd|active_enemies<4)
# filler :: Spread Moonfire
actions.bear+=/moonfire,cycle_targets=1,if=(talent.lunar_insight|talent.twin_moonfire)&active_enemies>1
actions.bear+=/swipe_bear

# [132127] :: Mangle to maintain Feline Potential stacks if present.
actions.ripweave+=/mangle,if=buff.feline_potential_counter.up&(buff.feline_potential_counter.remains<(gcd.remains+execute_time))
# Ripweave preparation once Feline Potential nears max stacks.
actions.ripweave+=/call_action_list,name=ripweave_prep,if=(buff.feline_potential_counter.stack>=(buff.feline_potential_counter.max_stack-1))&active_enemies=1

# [132127] :: Dump Rage with Maul before shifting to Cat Form.
actions.ripweave_prep+=/maul,if=!buff.feline_potential_counter.at_max_stacks&(variable.should_ironfur=0&variable.can_spend_rage_on_maul)
# [132127] :: Dump Rage with Raze before shifting to Cat Form.
actions.ripweave_prep+=/raze,if=!buff.feline_potential_counter.at_max_stacks&(variable.should_ironfur=0&variable.can_spend_rage_on_raze)
# [132127] :: Dump Rage with Ironfur before shifting to Cat Form.
actions.ripweave_prep+=/ironfur,use_off_gcd=1,if=!buff.feline_potential_counter.at_max_stacks&(variable.should_ironfur>0)
# [132127] :: Mangle to reach max stacks of Feline Potential.
actions.ripweave_prep+=/mangle,if=!buff.feline_potential_counter.at_max_stacks
# [132127] :: Thrash to maintain the Thrash DoT through time in Cat Form.
actions.ripweave_prep+=/thrash_bear,if=buff.feline_potential_counter.at_max_stacks&cooldown.mangle.remains>variable.cat_duration
# [132127] :: Stay in Bear Form and Maul until Mangle cooldown is ready after casting Ferocious Bite or Rip.
actions.ripweave_prep+=/maul,if=buff.feline_potential_counter.at_max_stacks&cooldown.mangle.remains>variable.cat_duration&(variable.should_ironfur=0&variable.can_spend_rage_on_maul)
# [132127] :: Stay in Bear Form and Raze until Mangle cooldown is ready after casting Ferocious Bite or Rip.
actions.ripweave_prep+=/raze,if=buff.feline_potential_counter.at_max_stacks&cooldown.mangle.remains>variable.cat_duration&(variable.should_ironfur=0&variable.can_spend_rage_on_raze)
# [132127] :: Stay in Bear Form and Swipe until Mangle cooldown is ready after casting Ferocious Bite or Rip.
actions.ripweave_prep+=/swipe_bear,if=buff.feline_potential_counter.at_max_stacks&cooldown.mangle.remains>variable.cat_duration
# [132127] :: Dump Rage with Ironfur before entering Cat Form.
actions.ripweave_prep+=/ironfur,use_off_gcd=1,if=buff.feline_potential_counter.at_max_stacks&cooldown.mangle.remains<=variable.cat_duration
# [132115] :: Shred to enter Cat Form if Rake shouldn't be refreshed yet.
actions.ripweave_prep+=/shred,if=buff.feline_potential_counter.at_max_stacks&!dot.rake.refreshable&buff.rage_of_the_sleeper.down
# [132115] :: Rake to enter Cat Form
actions.ripweave_prep+=/rake,if=buff.feline_potential_counter.at_max_stacks&buff.rage_of_the_sleeper.down

# [6035309] :: Thrash to generate Rage with Incarnation up.
actions.ursocs_guidance+=/thrash_bear
# [6035309] :: Mangle to generate Rage with Incarnation up.
actions.ursocs_guidance+=/mangle
# [6035309] :: Ironfur to shed Rage during Incarnation.
actions.ursocs_guidance+=/ironfur,use_off_gcd=1,if=variable.should_spend_rage>0

# dump :: Maul to prevent Rage from capping.
actions.dump_rage+=/maul,if=variable.should_ironfur=0&(variable.time_to_rage_cap<(gcd.remains+execute_time))
# dump :: Raze to prevent Rage from capping.
actions.dump_rage+=/raze,if=variable.should_ironfur=0&(variable.time_to_rage_cap<(gcd.remains+execute_time))
# dump :: Ironfur to prevent Rage from capping.
actions.dump_rage+=/ironfur,use_off_gcd=1,if=(variable.time_to_rage_cap<gcd.remains)

actions.spend_rage+=/maul,if=variable.can_spend_rage_on_maul>0
actions.spend_rage+=/raze,if=variable.can_spend_rage_on_raze>0
