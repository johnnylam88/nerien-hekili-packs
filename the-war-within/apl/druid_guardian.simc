# Guardian Druid Action Priority List
# Based on *Guardian Druid Tank Guide* by Pumps
# [https://www.icy-veins.com/wow/guardian-druid-pve-tank-guide]
#
# Recommended Settings for `/hekili` > *Guardian*:
#
# - Weave Cat Form and Bear Form : enabled
# - Maul (or Raze) Rage Threshold : 0
# - Nature's Vigil Damage Threshold : 70
# - Ironfur Damage Threshold : 5
# - Ironfur Maximum Stacks : 14

actions.precombat+=/mark_of_the_wild
actions.precombat+=/heart_of_the_wild,if=!(druid.catweave_bear&talent.rip&talent.rake&talent.fluid_form&talent.wildpower_surge)
actions.precombat+=/bear_form,if=time=0&buff.feline_potential.down&buff.feline_potential_counter.stack<6
actions.precombat+=/use_item,name=swarmlords_authority,if=buff.bear_form.up&(buff.berserk_bear.down&buff.incarnation.down)
actions.precombat+=/use_item,name=chromebustible_bomb_suit,if=buff.bear_form.up&(buff.berserk_bear.down&buff.incarnation.down)
actions.precombat+=/use_item,name=ringing_ritual_mud,if=buff.bear_form.up&(buff.berserk_bear.down&buff.incarnation.down)

### Constants ###

actions+=/variable,name=rage_cost_frenzied_regeneration,value=10
actions+=/variable,name=rage_cost_ironfur,value=40
actions+=/variable,name=rage_cost_maul,value=40
actions+=/variable,name=rage_cost_raze,value=40
actions+=/variable,name=rage_cost_max,value=((variable.rage_cost_ironfur<?variable.rage_cost_maul)<?variable.rage_cost_raze)

actions+=/variable,name=rage_gain_mangle,value=(12+5*talent.soul_of_the_forest)
actions+=/variable,name=rage_gain_thrash_bear,value=5

actions+=/variable,name=cooldown_mangle_duration,value=6*spell_haste
actions+=/variable,name=cooldown_thrash_bear_duration,value=6*spell_haste

### Tunables ###

# Pool enough Rage to cast Ironfur and Frenzied Regeneration.
actions+=/variable,name=rage_pool_value,value=variable.rage_cost_ironfur+variable.rage_cost_frenzied_regeneration
# Barkskin tunable for incoming damage.
actions+=/variable,name=barkskin_damage_taken,value=0.2*health.max
# Frenzied Regeneration tunable for minimum health percent.
actions+=/variable,name=frenzied_regeneration_health_pct,value=50
# Ironfur tunable for incoming damage per suggested application (see /hekili > Guardian).
actions+=/variable,name=ironfur_damage_taken,value=ironfur_damage_threshold
# Nature's Vigil tunable for minimum health percent (see /hekili > Guardian).
actions+=/variable,name=natures_vigil_health_pct,value=vigil_damage
# Renewal tunable for minimum health percent.
actions+=/variable,name=renewal_health_pct,value=65
# Regrowth tunable for minimum health percent.
actions+=/variable,name=regrowth_health_pct,value=85
# Survival Instincts tunable for incoming damage.
actions+=/variable,name=survival_instincts_damage_taken,value=0.2*health.max

################

# ripweave is 1 if the conditions for Ripweaving are true (Rip, Rake, Fluid Form, and Wildpower Surge are talented).
actions+=/variable,name=ripweave,value=1,value_else=0,op=setif,condition=druid.catweave_bear&talent.rip&talent.rake&talent.fluid_form&talent.wildpower_surge

# 4 Rage per auto-attack.
actions+=/variable,name=rage_per_second_auto_attack,value=4%(2*attack_haste)
# 3 Rage per incoming auto-attack, and incoming auto-attack every 2s with 1 mob.
# Assume at 1 mob, we don't get any rage, but at 5+ mobs that we gain 3 Rage per second.
actions+=/variable,name=rage_per_second_incoming_auto_attack,value=(3%4)*(active_enemies-1),value_else=3,op=setif,condition=active_enemies<5
actions+=/variable,name=rage_per_second_incoming_auto_attack,value=(3%2)+((3%2)%(5-1))*(active_enemies-1),value_else=3,op=setif,condition=active_enemies<5
# Blood Frenzy causes the Thrash DoT to generate 3 Rage per tick on up to 5 targets.
actions+=/variable,name=rage_per_second_blood_frenzy,value=talent.blood_frenzy*(3%dot.thrash_bear.tick_time)*(active_dot.thrash_bear<?5)
# Galactic Guardian causes damage to have a 5% chance to trigger a free Moonfire on that target, and the next Moonfire cast generates 8 Rage.
# This underestimates the Rage generated because we only factor in Thrash ticks to proc Galactic Guardian.
actions+=/variable,name=rage_per_second_galactic_guardian,value=talent.galactic_guardian*8*0.05*(active_dot.thrash_bear%dot.thrash_bear.tick_time)
# Moon Guardian causes the free Moonfires from Galactic Guardian to generate 5 Rage.
# Galactic Guardian causes damage to have a 5% chance to trigger a free Moonfire on that target.
# This underestimates the Rage generated because we only factor in Thrash ticks to proc Galactic Guardian.
actions+=/variable,name=rage_per_second_moon_guardian,value=talent.moon_guardian*talent.galactic_guardian*5*0.05*(active_dot.thrash_bear%dot.thrash_bear.tick_time)
# This underestimates the Rage from Mangle because we don't account for the Gore talent.
actions+=/variable,name=rage_per_second_mangle,value=variable.rage_gain_mangle%variable.cooldown_mangle_duration
actions+=/variable,name=rage_per_second_thrash_bear,value=variable.rage_gain_thrash_bear%variable.cooldown_thrash_bear_duration
actions+=/variable,name=rage_per_second,value=variable.rage_per_second_auto_attack+variable.rage_per_second_incoming_auto_attack+variable.rage_per_second_blood_frenzy+variable.rage_per_second_galactic_guardian+variable.rage_per_second_moon_guardian+variable.rage_per_second_mangle+variable.rage_per_second_thrash_bear
actions+=/variable,name=rage_at_next_gcd,value=rage+gcd*variable.rage_per_second
# should_spend_rage is 1 if spending Rage in the next GCD won't drop below the pool value.
actions+=/variable,name=should_spend_rage,value=1,op=setif,condition=variable.rage_at_next_gcd-variable.rage_cost_max>=variable.rage_pool_value

actions+=/variable,name=rage_check,value=1,value_else=0,op=setif,condition=rage+variable.rage_per_second*gcd>rage.max
# Mangle with Gore generates 4 additional Rage.
actions+=/variable,name=rage_check_mangle,value=1,value_else=0,op=setif,condition=cooldown.mangle.remains<gcd&rage+variable.rage_gain_mangle+4*buff.gore.up>rage.max
# Moonfire with Galactic Guardian generates 8 Rage.
actions+=/variable,name=rage_check_moonfire,value=1,value_else=0,op=setif,condition=rage+8*buff.galactic_guardian.up>rage.max
actions+=/variable,name=rage_check_thrash_bear,value=1,value_else=0,op=setif,condition=cooldown.thrash_bear.remains<gcd&rage+variable.rage_gain_thrash_bear+4*buff.gore.up>rage.max
# should_dump_rage is 1 if an ability is coming off cooldown and will overcap on Rage.
actions+=/variable,name=should_dump_rage,value=1,value_else=0,op=setif,condition=variable.rage_check>0|variable.rage_check_moonfire>0|variable.rage_check_mangle>0|variable.rage_check_thrash_bear>0

# Ironfur if not already up, about to expire, or we need more stacks.
actions+=/variable,name=ironfur_threshold,value=floor(incoming_physical_damage_5s%variable.ironfur_damage_taken)
actions+=/variable,name=should_ironfur,value=1,value_else=0,op=setif,condition=tanking&(buff.ironfur.down|buff.ironfur.remains<2|buff.ironfur.stack<variable.ironfur_threshold)

# berserk_ready is 1 if Berserk or Incarnation is imminent.
actions+=/variable,name=berserk_ready,value=1,value_else=0,op=setif,condition=(talent.incarnation&cooldown.incarnation.remains<gcd)|(!talent.incarnation&cooldown.berserk_bear.remains<gcd)

# Several of the offensive cooldowns give a defensive benefit.
actions+=/variable,name=has_offensive_defensive_buff,value=1,value_else=0,op=setif,condition=buff.lunar_beam.up|buff.rage_of_the_sleeper.up|buff.berserk_bear.up|buff.incarnation.up
actions+=/variable,name=has_defensive_buff,value=1,value_else=0,op=setif,condition=buff.barkskin.up|buff.survival_instincts.up|buff.chromebustible_bomb_suit.up|variable.has_offensive_defensive_buff>0
actions+=/variable,name=has_absorb_buff,value=1,value_else=0,op=setif,condition=buff.chromebustible_bomb_suit.up|buff.mudborne.up

################

actions+=/skull_bash
actions+=/soothe
# [132276] :: Mangle to switch back to Bear Form.
actions+=/mangle,if=buff.bear_form.down&talent.fluid_form&buff.feline_potential.down
actions+=/bear_form,if=!talent.fluid_form&buff.feline_potential.down
actions+=/call_action_list,name=mitigation,if=tanking
actions+=/call_action_list,name=defensives,if=tanking
actions+=/call_action_list,name=heal
actions+=/run_action_list,name=bear,if=buff.bear_form.up
actions+=/run_action_list,name=cat,if=buff.cat_form.up

actions.defensives+=/use_item,name=swarmlords_authority,use_off_gcd=1,if=incoming_damage_5s>0&(buff.berserk_bear.down&buff.incarnation.down)&variable.berserk_ready=0&variable.has_absorb_buff=0
actions.defensives+=/use_item,name=ringing_ritual_mud,use_off_gcd=1,if=incoming_damage_5s>0&(buff.berserk_bear.down&buff.incarnation.down)&variable.berserk_ready=0&variable.has_absorb_buff=0
actions.defensives+=/barkskin,use_off_gcd=1,if=incoming_damage_5s>=variable.barkskin_damage_taken&variable.has_defensive_buff=0
actions.defensives+=/use_item,name=chromebustible_bomb_suit,use_off_gcd=1,if=incoming_damage_5s>=variable.barkskin_damage_taken&(buff.berserk_bear.down&buff.incarnation.down)&variable.berserk_ready=0&variable.has_defensive_buff=0&variable.has_absorb_buff=0
actions.defensives+=/survival_instincts,use_off_gcd=1,if=incoming_damage_5s>=variable.survival_instincts_damage_taken&variable.has_defensive_buff=0&variable.has_absorb_buff=0

actions.heal+=/natures_vigil,if=health.pct<variable.natures_vigil_health_pct
actions.heal+=/regrowth,if=buff.dream_of_cenarius.up&health.pct<variable.regrowth_health_pct
actions.heal+=/frenzied_regeneration,if=buff.frenzied_regeneration.down&health.pct<variable.frenzied_regeneration_health_pct&charges_fractional>0.8
actions.heal+=/renewal,if=health.pct<variable.renewal_health_pct

actions.mitigation+=/ironfur,use_off_gcd=1,if=variable.should_ironfur>0

actions.racials+=/berserking

# cat_duration is the minimum amount of time spent in Cat Form.
actions+=/variable,name=cat_duration,op=reset,default=0
actions+=/variable,name=cat_duration,op=add,value=action.rake.execute_time,if=talent.rake
actions+=/variable,name=cat_duration,op=add,value=action.heart_of_the_wild.execute_time,if=talent.heart_of_the_wild&cooldown.heart_of_the_wild.ready
actions+=/variable,name=cat_duration,op=add,value=action.convoke_the_spirits.execute_time,if=talent.convoke_the_spirits&cooldown.convoke_the_spirits.ready
actions+=/variable,name=cat_duration,op=add,value=action.rip.execute_time,if=talent.rip

# [132115] :: Heart of the Wild to increase damage in Cat Form.
actions.cat+=/heart_of_the_wild
# [132115] :: Convoke the Spirits for damage in Cat Form.
actions.cat+=/convoke_the_spirits
# [132115] :: Ferocious Bite if Rip shouldn't be refreshed yet.
actions.cat+=/ferocious_bite,if=combo_points=5&(buff.berserk_bear.up|buff.incarnation.up)&!dot.rip.refreshable
# [132115] :: Apply or refresh Rip.
actions.cat+=/rip,if=combo_points=5
# [132115] :: Shred if Rake shouldn't be refreshed yet.
actions.cat+=/shred,if=!dot.rake.refreshable
# [132115] :: Apply or refresh Rake.
actions.cat+=/rake

# [132143] :: Maintain max stacks of Thrash for DR from Rend and Tear.
actions.bear+=/thrash_bear,if=debuff.thrash_bear.stack<debuff.thrash_bear.max_stack
actions.bear+=/moonfire,if=(!ticking|refreshable)&target.time_to_die>12
# [132127] :: Mangle to maintain Feline Potential stacks if present.
actions.bear+=/mangle,if=variable.ripweave>0&buff.feline_potential_counter.up&buff.feline_potential_counter.remains<2
# Ripweave preparation once Feline Potential nears max stacks.
actions.bear+=/call_action_list,name=ripweave_prep,if=variable.ripweave>0&buff.feline_potential_counter.stack>=5&active_enemies<2
actions.bear+=/heart_of_the_wild,if=variable.ripweave=0
actions.bear+=/convoke_the_spirits,if=variable.ripweave=0
actions.bear+=/berserk_bear
actions.bear+=/incarnation
actions.bear+=/call_action_list,name=racials,if=buff.berserk_bear.up|buff.incarnation.up
actions.bear+=/potion,if=buff.berserk_bear.up|buff.incarnation.up
actions.bear+=/lunar_beam
actions.bear+=/rage_of_the_sleeper
# [134298] :: Maul is free with Tooth and Claw proc.
actions.bear+=/maul,if=buff.tooth_and_claw.up
# [134298] :: Raze is free with Tooth and Claw proc.
actions.bear+=/raze,if=buff.tooth_and_claw.up
actions.bear+=/call_action_list,name=spend_rage,if=variable.should_ironfur=0&variable.should_spend_rage>0
actions.bear+=/thrash_bear
actions.bear+=/mangle
# [135853] :: Moonfire with Galactic Guardian
actions.bear+=/moonfire,if=buff.galactic_guardian.up&(buff.galactic_guardian.remains<action.moonfire.execute_time+gcd|active_enemies<4)
# filler :: Spread Moonfire
actions.bear+=/moonfire,cycle_targets=1,if=(talent.lunar_insight|talent.twin_moonfire)&active_enemies>1
actions.bear+=/swipe_bear

# [132127] :: Dump Rage before shifting to Cat Form.
actions.ripweave_prep+=/ironfur,use_off_gcd=1,if=rage-variable.rage_cost_ironfur>variable.rage_cost_max&buff.feline_potential_counter.stack<6&variable.should_ironfur>0
# [132127] :: Dump Rage before shifting to Cat Form.
actions.ripweave_prep+=/maul,if=rage-variable.rage_cost_maul>variable.rage_cost_max&buff.feline_potential_counter.stack<6&variable.should_ironfur=0
# [132127] :: Dump Rage before shifting to Cat Form.
actions.ripweave_prep+=/raze,if=rage-variable.rage_cost_raze>variable.rage_cost_max&buff.feline_potential_counter.stack<6&variable.should_ironfur=0
# [132127] :: Mangle to get max stacks of Feline Potential.
actions.ripweave_prep+=/mangle,if=buff.feline_potential_counter.stack<6
# [132127] :: Thrash to maintain the Thrash DoT through time in Cat Form.
actions.ripweave_prep+=/thrash_bear,if=buff.feline_potential_counter.stack=6&cooldown.mangle.remains>variable.cat_duration
# [132127] :: Stay in Bear Form and Ironfur until Mangle cooldown is ready after casting Ferocious Bite or Rip.
actions.ripweave_prep+=/ironfur,use_off_gcd=1,if=buff.feline_potential_counter.stack=6&cooldown.mangle.remains>variable.cat_duration&variable.should_ironfur>0
# [132127] :: Stay in Bear Form and Maul until Mangle cooldown is ready after casting Ferocious Bite or Rip.
actions.ripweave_prep+=/maul,if=buff.feline_potential_counter.stack=6&cooldown.mangle.remains>variable.cat_duration&variable.should_ironfur=0
# [132127] :: Stay in Bear Form and Raze until Mangle cooldown is ready after casting Ferocious Bite or Rip.
actions.ripweave_prep+=/raze,if=buff.feline_potential_counter.stack=6&cooldown.mangle.remains>variable.cat_duration&variable.should_ironfur=0
# [132127] :: Stay in Bear Form and Swipe until Mangle cooldown is ready after casting Ferocious Bite or Rip.
actions.ripweave_prep+=/swipe_bear,if=buff.feline_potential_counter.stack=6&cooldown.mangle.remains>variable.cat_duration
# [132115] :: Shred to enter Cat Form if Rake shouldn't be refreshed yet.
actions.ripweave_prep+=/shred,if=buff.feline_potential_counter.stack=6&!dot.rake.refreshable
# [132115] :: Rake to enter Cat Form
actions.ripweave_prep+=/rake,if=buff.feline_potential_counter.stack=6

actions.spend_rage+=/maul
actions.spend_rage+=/raze
