# Protection Warrior Priority List
# Based on:
# - *Protection Warrior Tank Guide* by Nomeratur
#   [https://www.wowhead.com/guide/classes/warrior/protection/overview-pve-tank]
# - *Protection Warrior Tank Guide* by Mwahi
#   [https://www.icy-veins.com/wow/protection-warrior-pve-tank-guide]
# - *Yoda's Prot warrior guide* by Yoda
#   [https://docs.google.com/document/d/1iomJ1BcyBc4Fy1HFIfiuAeL_B5t6kioRW4DBPR1MPgg]
#
# Recommended Settings in `/hekili` > *Protection*:
#
# - Only Shockwave as Interrupt : **unused**
# - Overlap Shield Block : checked
# - Allow Stance Changes : unchecked
# - Reserve Rage for Mitigation : 0
# - Shield Wall Damage Required : 20
# - Shield Wall Health Percentage : 80
# - Rallying Cry Damage Required : 20
# - Rallying Cry Health Percentage : 80
# - Last Stand Damage Required : 20
# - Last Stand Health Percentage : 80
# - Victory Rush Health Threshold : 75

actions.precombat+=/defensive_stance,if=!buff.defensive_stance.up
actions.precombat+=/battle_shout
actions.precombat+=/use_item,name=swarmlords_authority
actions.precombat+=/use_item,name=chromebustible_bomb_suit
actions.precombat+=/use_item,name=ringing_ritual_mud

actions+=/variable,name=rage_cost_execute,value=40
actions+=/variable,name=rage_cost_ignore_pain,value=35
actions+=/variable,name=rage_cost_revenge,value=20
actions+=/variable,name=rage_cost_shield_block,value=30

actions+=/variable,name=seeing_red_execute,value=100-(variable.rage_cost_execute%5)
actions+=/variable,name=seeing_red_ignore_pain,value=100-(variable.rage_cost_ignore_pain%5)
actions+=/variable,name=seeing_red_revenge,value=100-(variable.rage_cost_revenge%5)
actions+=/variable,name=seeing_red_shield_block,value=100-(variable.rage_cost_shield_block%5)

actions+=/variable,name=rage_gain_shield_slam,value=(15+2*talent.heavy_repercussions+3*talent.practiced_strikes)
actions+=/variable,name=rage_gain_thunder_blast,value=8
actions+=/variable,name=rage_gain_thunder_clap,value=5

### Tunables ###

# Set a soft cap for maximum Rage.
# This value is used when checking if we'll cap on Rage.
# It's less than maximum Rage because our Rage generation estimates tend to
# underestimate the true Rage generation due to procs, and comparing against
# the true max Rage has a higher probability of capping.
actions+=/variable,name=rage_cap,value=(rage.max-10)
# Always maintain enough Rage for Ignore Pain or Shield Block.
actions+=/variable,name=rage_floor,value=(variable.rage_cost_ignore_pain<?variable.rage_cost_shield_block)
# Pool enough Rage to cast two of Ignore Pain or Shield Block.
actions+=/variable,name=rage_pool,value=2*(variable.rage_cost_ignore_pain<?variable.rage_cost_shield_block)
# Don't cast Ignore Pain if we will waste too much of the casted absorb.
actions+=/variable,name=ignore_pain_wasted_pct,value=20
# Demoralizing Shout tunable for incoming damage.
actions+=/variable,name=demoralizing_shout_damage_taken,value=0.2*health.max
# Last Stand tunable for incoming damage (see /hekili > Protection).
actions+=/variable,name=last_stand_damage_taken,value=last_stand_damage_taken
# Last Stand tunable for minimum health percent (see /hekili > Protection).
actions+=/variable,name=last_stand_health_pct,value=last_stand_health_pct
# Rallying Cry tunable for incoming damage (see /hekili > Protection).
actions+=/variable,name=rallying_cry_damage_taken,value=rallying_cry_damage_taken
# Rallying Cry tunable for minimum health percent (see /hekili > Protection).
actions+=/variable,name=rallying_cry_health_pct,value=rallying_cry_health_pct
# Shield Wall tunable for incoming damage (see /hekili > Protection).
actions+=/variable,name=shield_wall_damage_taken,value=shield_wall_damage_taken
# Victory Rush tunable for minimum health percent (see /hekili > Protection).
actions+=/variable,name=victory_rush_health_pct,value=victory_rush_health_pct
# Minimum percent of mobs that must have Demoralizing Shout to avoid overlapping another defensive.
actions+=/variable,name=demoralizing_shout_pct_threshold,value=75

################

# Amount of Rage until the Rage cap is reached.
actions+=/variable,name=rage_deficit,value=(variable.rage_cap-rage)

# has_deep_wounds_threat is 1 if the percentage of enemy mobs with the Deep Wounds DoT on them exceeds 75%.
actions+=/variable,name=has_deep_wounds_threat,value=1,value_else=0,op=setif,condition=(active_dot.deep_wounds%active_enemies*100)>75
# has_rend_threat is 1 if the percentage of enemy mobs with the Rend DoT on them exceeds 75%.
actions+=/variable,name=has_rend_threat,value=1,value_else=0,op=setif,condition=(active_dot.rend%active_enemies*100)>75

# XXX This curving of the multiplier is goofy.
actions+=/variable,name=rage_gen_multiplier,value=1+0.5*buff.unnerving_focus.remains%buff.unnerving_focus.duration
actions+=/variable,name=rage_gen_multiplier_outburst,value=1.5,value_else=1,op=setif,condition=buff.violent_outburst.up
actions+=/variable,name=rage_per_second_auto_attack,value=1*(1+0.5*talent.war_machine)+2*talent.devastator*talent.instigate
# 3 Rage per incoming auto-attack, and incoming auto-attack every 2s with 1 mob.
# Assume at 1 mob, we don't get any rage, but at 5+ mobs, that we gain 3 Rage every second.
actions+=/variable,name=rage_per_second_incoming_auto_attack,value=(3%4)*(spell_targets.thunder_clap-1),value_else=3,op=setif,condition=spell_targets.thunder_clap<5
actions+=/variable,name=rage_per_second_incoming_damage,value=variable.rage_per_second_incoming_auto_attack,value_else=0,op=setif,condition=incoming_physical_damage_5s>0
# Bloodsurge has a 10% chance to gain 5 rage per Bleed tick.
actions+=/variable,name=rage_per_second_bloodsurge,value=talent.bloodsurge*(0.1*5%3)*active_dot.deep_wounds
# Ravager ticks every 2s for 12s (hasted) for 10 rage every time it does damage.
actions+=/variable,name=rage_per_second_ravager,value=buff.ravager.up*5
# This underestimates the rage from Shield Slam because we don't account for Shield Slam resets.
actions+=/variable,name=rage_per_second_shield_slam,value=variable.rage_gain_shield_slam%cooldown.shield_slam.duration
# This underestimates the rage from Thunder Clap because we don't account for Thunder Blast.
actions+=/variable,name=rage_per_second_thunder_clap,value=variable.rage_gain_thunder_clap%cooldown.thunder_clap.duration
actions+=/variable,name=rage_per_second,value=(variable.rage_per_second_auto_attack+variable.rage_per_second_incoming_damage+variable.rage_per_second_bloodsurge+variable.rage_per_second_ravager+variable.rage_per_second_shield_slam+variable.rage_per_second_thunder_clap)*variable.rage_gen_multiplier
actions+=/variable,name=rage_at_next_shield_block,value=rage,value_else=rage+cooldown.shield_block.remains*variable.rage_per_second,op=setif,condition=cooldown.shield_block.charges>0
# should_spend_rage is 1 if the expected Rage by the next Shield Block exceeds the pool value.
actions+=/variable,name=should_spend_rage,value=1,value_else=0,op=setif,condition=(variable.rage_at_next_shield_block>variable.rage_pool)
# can_spend_rage_on_execute is 1 if casting Execute doesn't drop Rage below the floor value.
actions+=/variable,name=can_spend_rage_on_execute,value=1,value_else=0,op=setif,condition=(rage-variable.rage_cost_execute>=variable.rage_floor)
# can_spend_rage_on_revenge is 1 if casting Revenge doesn't drop Rage below the floor value.
actions+=/variable,name=can_spend_rage_on_revenge,value=1,value_else=0,op=setif,condition=(rage-variable.rage_cost_revenge>=variable.rage_floor)

# time_to_rage_cap is the number of seconds before a Rage-generating ability comes off cooldown and will cap total Rage if cast.
actions+=/variable,name=time_to_rage_cap,op=reset,default=(variable.rage_deficit%variable.rage_per_second)
actions+=/variable,name=time_to_rage_cap,op=min,value=cooldown.avatar.remains,if=talent.avatar&((cooldown.avatar.remains*variable.rage_per_second+10*variable.rage_gen_multiplier)>=variable.rage_deficit)
actions+=/variable,name=time_to_rage_cap,op=min,value=cooldown.champions_spear.remains,if=talent.champions_spear&((cooldown.champions_spear.remains*variable.rage_per_second+10*(1+talent.piercing_challenge)*variable.rage_gen_multiplier)>=variable.rage_deficit)
actions+=/variable,name=time_to_rage_cap,op=min,value=cooldown.demoralizing_shout.remains,if=talent.demoralizing_shout&talent.booming_voice&((cooldown.demoralizing_shout.remains*variable.rage_per_second+20*variable.rage_gen_multiplier)>=variable.rage_deficit)
actions+=/variable,name=time_to_rage_cap,op=min,value=cooldown.shield_charge.remains,if=talent.shield_charge&((cooldown.shield_charge.remains*variable.rage_per_second+20*variable.rage_gen_multiplier)>=variable.rage_deficit)
actions+=/variable,name=time_to_rage_cap,op=min,value=cooldown.shield_slam.remains,if=((cooldown.shield_slam.remains*variable.rage_per_second+variable.rage_gain_shield_slam*variable.rage_gen_multiplier_outburst*variable.rage_gen_multiplier)>=variable.rage_deficit)
actions+=/variable,name=time_to_rage_cap,op=min,value=cooldown.thunder_blast.remains,if=talent.thunder_blast&buff.thunder_blast.up&((cooldown.thunder_blast.remains*variable.rage_per_second+variable.rage_gain_thunder_blast*variable.rage_gen_multiplier_outburst*variable.rage_gen_multiplier)>=variable.rage_deficit)
actions+=/variable,name=time_to_rage_cap,op=min,value=cooldown.thunder_clap.remains,if=talent.thunder_clap&((cooldown.thunder_clap.remains*variable.rage_per_second+variable.rage_gain_thunder_clap*variable.rage_gen_multiplier_outburst*variable.rage_gen_multiplier)>=variable.rage_deficit)

actions+=/variable,name=has_demoralizing_shout_safety,value=1,value_else=0,op=setif,condition=(active_dot.demoralizing_shout%active_enemies*100>=variable.demoralizing_shout_pct_threshold)

actions+=/variable,name=has_absorb_buff,value=1,value_else=0,op=setif,condition=buff.chromebustible_bomb_suit.up|buff.mudborne.up
actions+=/variable,name=has_defensive_buff,value=1,value_else=0,op=setif,condition=variable.has_demoralizing_shout_safety>0|buff.shield_wall.up|buff.chromebustible_bomb_suit.up

actions+=/variable,name=ignore_pain_cap,value=0.3*health.max
actions+=/variable,name=ignore_pain_absorb,value=buff.ignore_pain.v1,value_else=0,op=setif,condition=buff.ignore_pain.up
# Magic constants to estimate the absorb granted by casting Ignore Pain.
actions+=/variable,name=ignore_pain_absorb_oncast,value=stat.attack_power*5.75*(1+0.2*talent.no_stranger_to_pain)*(1+stat.versatility_atk_mod)*2
# should_ignore_pain is 1 if we are keeping up the Ignore Pain absorb; keep adding to Ignore Pain if incoming damage exceeds the existing absorb amount.
actions+=/variable,name=should_ignore_pain,value=1,value_else=0,op=setif,condition=buff.ignore_pain.down|buff.ignore_pain.remains<2|(variable.has_absorb_buff=0&(variable.ignore_pain_cap-variable.ignore_pain_absorb)>(variable.ignore_pain_absorb_oncast*(1-variable.ignore_pain_wasted_pct%100))&variable.has_deep_wounds_threat>0&incoming_damage_5s>variable.ignore_pain_absorb)

actions+=/variable,name=should_execute,value=1,value_else=0,op=setif,condition=(talent.heavy_handed&spell_targets.revenge<=6)|spell_targets.revenge<=3

actions+=/pummel
actions+=/spell_reflection
actions+=/defensive_stance,if=!buff.defensive_stance.up&health.pct<70&incoming_damage_3s>0.2*health.max
actions+=/call_action_list,name=opener,if=time<=3*gcd.max
actions+=/call_action_list,name=mitigation,if=tanking
actions+=/call_action_list,name=defensives,if=tanking
actions+=/call_action_list,name=heal
actions+=/call_action_list,name=cooldowns
actions+=/battle_stance,if=!buff.battle_stance.up&health.pct>70
actions+=/run_action_list,name=standard

actions.cooldowns+=/use_item,name=tome_of_lights_devotion,use_off_gcd=1,if=buff.inner_resilience.up&buff.resilience_verses.stack_pct<50
actions.cooldowns+=/potion,if=!talent.avatar|buff.avatar.remains>8
actions.cooldowns+=/use_items,if=!talent.avatar|buff.avatar.remains>8
actions.cooldowns+=/call_action_list,name=racials,if=!talent.avatar|buff.avatar.remains>8

actions.defensives+=/use_item,name=swarmlords_authority,use_off_gcd=1,if=incoming_damage_5s>0&variable.has_absorb_buff=0
actions.defensives+=/use_item,name=ringing_ritual_mud,use_off_gcd=1,if=incoming_damage_5s>0&variable.has_absorb_buff=0
actions.defensives+=/demoralizing_shout,if=!talent.booming_voice&incoming_damage_5s>variable.demoralizing_shout_damage_taken&variable.has_defensive_buff=0
actions.defensives+=/use_item,name=chromebustible_bomb_suit,use_off_gcd=1,if=incoming_damage_5s>variable.shield_wall_damage_taken&variable.has_defensive_buff=0&variable.has_absorb_buff=0
actions.defensives+=/shield_wall,use_off_gcd=1,if=(!talent.immovable_object|!cooldown.avatar.ready)&incoming_damage_5s>variable.shield_wall_damage_taken&variable.has_defensive_buff=0&variable.has_absorb_buff=0
actions.defensives+=/last_stand,use_off_gcd=1,if=!talent.bolster&(incoming_damage_5s>variable.last_stand_damage_taken&health.pct<variable.last_stand_health_pct)&buff.rallying_cry.down
actions.defensives+=/rallying_cry,use_off_gcd=1,if=(incoming_damage_5s>variable.rallying_cry_damage_taken&health.pct<variable.rallying_cry_health_pct)&buff.last_stand.down

actions.heal+=/victory_rush,if=buff.victorious.up&health.pct<variable.victory_rush_health_pct
actions.heal+=/impending_victory,if=health.pct<variable.victory_rush_health_pct

actions.mitigation+=/shield_block,if=buff.shield_block.down|buff.shield_block.remains<4
# [132110] :: Last Stand with Bolster to apply Shield Block.
actions.mitigation+=/last_stand,if=talent.bolster&buff.shield_block.down
# [841383] :: Ignore Pain to trigger Violent Outburst.
actions.mitigation+=/ignore_pain,use_off_gcd=1,if=variable.should_ignore_pain>0&variable.should_spend_rage>0&(buff.seeing_red.stack>=variable.seeing_red_ignore_pain&cooldown.shield_slam.ready)
actions.mitigation+=/ignore_pain,use_off_gcd=1,if=variable.should_ignore_pain>0

# Put up Shield Block as quickly as possible and debuff mobs with Demoralizing Shout at the start of a pull.
actions.opener+=/shield_block,use_off_gcd=1,if=buff.shield_block.down
actions.opener+=/ravager
actions.opener+=/charge,use_off_gcd=1,if=movement.distance>8
actions.opener+=/avatar,use_off_gcd=1
# [132110] :: Shield Charge to generate Rage for or apply Shield Block.
actions.opener+=/shield_charge,if=buff.shield_block.down|rage<variable.rage_cost_shield_block
actions.opener+=/demoralizing_shout

# [236308] :: Thunder Blast to consume Violent Outburst in multi-target.
actions.outburst+=/thunder_blast,if=buff.violent_outburst.up&spell_targets.thunder_clap>=3
# [236308] :: Shield Slam to consume Violent Outburst.
actions.outburst+=/shield_slam,if=buff.violent_outburst.up
# [236308] :: Thunder Blast to consume Violent Outburst in multi-target.
actions.outburst+=/thunder_blast,if=buff.violent_outburst.up&cooldown.shield_slam.remains>2&spell_targets.thunder_clap>=2
# [236308] :: Thunder Clap to consume Violent Outburst in multi-target.
actions.outburst+=/thunder_clap,if=buff.violent_outburst.up&cooldown.shield_slam.remains>2&spell_targets.thunder_clap>=2
# [841383] :: Put Thunder Clap on cooldown so Violent Outburst is consumed by Shield Slam instead in single-target.
actions.outburst+=/thunder_clap,if=((buff.seeing_red.stack>=variable.seeing_red_execute)|(buff.seeing_red.stack>=variable.seeing_red_revenge)|(buff.seeing_red.stack>=variable.seeing_red_ignore_pain))&spell_targets.thunder_clap<3
# [841383] :: Execute to trigger Violent Outburst.
actions.outburst+=/execute,if=variable.should_ignore_pain=0&(variable.should_spend_rage>0&variable.can_spend_rage_on_execute>0)&(buff.seeing_red.stack>=variable.seeing_red_execute&cooldown.shield_slam.remains<(gcd.remains+execute_time))&variable.should_execute>0
# [841383] :: Revenge to trigger Violent Outburst in multi-target.
actions.outburst+=/revenge,if=variable.should_ignore_pain=0&(variable.should_spend_rage>0&variable.can_spend_rage_on_revenge>0)&(buff.seeing_red.stack>=variable.seeing_red_revenge&cooldown.shield_slam.remains<(gcd.remains+execute_time))

actions.racials+=/blood_fury
actions.racials+=/berserking
#actions.racials+=/arcane_torrent
actions.racials+=/lights_judgment
actions.racials+=/ancestral_call
#actions.racials+=/fireblood
actions.racials+=/bag_of_tricks

# [132090] :: Revenge to apply Deep Wounds
actions.ravager_up+=/revenge,if=debuff.deep_wounds.down|variable.has_deep_wounds_threat=0
# [970854] :: Shield Slam during Ravager.
actions.ravager_up+=/shield_slam
# [970854] :: Execute during Ravager when possible.
actions.ravager_up+=/execute,if=((variable.time_to_rage_cap<(gcd.remains+execute_time))|(variable.should_spend_rage>0&variable.can_spend_rage_on_execute>0))&variable.should_execute>0
# [970854] :: Revenge during Ravager.
actions.ravager_up+=/revenge,if=((variable.time_to_rage_cap<(gcd.remains+execute_time))|(variable.should_spend_rage>0&variable.can_spend_rage_on_revenge>0))

# dump :: Revenge to prevent Rage from capping with at least 5 stacks of Colossal Might.
actions.dump_rage+=/revenge,if=(variable.time_to_rage_cap<(gcd.remains+execute_time))&(talent.tide_of_battle&buff.colossal_might.stack>=5)
# dump :: Execute to prevent Rage from capping.
actions.dump_rage+=/execute,if=(variable.time_to_rage_cap<(gcd.remains+execute_time))&variable.should_execute>0
# dump :: Revenge to prevent Rage from capping.
actions.dump_rage+=/revenge,if=(variable.time_to_rage_cap<(gcd.remains+execute_time))
# dump :: Ignore Pain to prevent Rage from capping.
actions.dump_rage+=/ignore_pain,use_off_gcd=1,if=(variable.time_to_rage_cap<gcd.remains)

# Prefer Revenge to spend Rage with at least 5 stacks of Colossal Might.
actions.spend_rage+=/revenge,if=variable.can_spend_rage_on_revenge>0&talent.tide_of_battle&buff.colossal_might.stack>=5
actions.spend_rage+=/execute,if=variable.can_spend_rage_on_execute>0&variable.should_execute>0
actions.spend_rage+=/revenge,if=variable.can_spend_rage_on_revenge>0

actions.standard+=/charge,if=movement.distance>8
# [613534] :: Thunder Blast during Avatar.
actions.standard+=/thunder_blast,if=talent.unstoppable_force&buff.avatar.up
# [613534] :: Thunder Clap during Avatar.
actions.standard+=/thunder_clap,if=talent.unstoppable_force&buff.avatar.up
actions.standard+=/ravager
actions.standard+=/avatar,if=buff.avatar.down
actions.standard+=/demoralizing_shout,if=talent.booming_voice
actions.standard+=/demolish,if=buff.colossal_might.stack>=3
actions.standard+=/champions_spear
actions.standard+=/thunderous_roar
actions.standard+=/shield_charge
# free :: Execute when Sudden Death is at max stacks.
actions.standard+=/execute,if=talent.sudden_death&buff.sudden_death.at_max_stacks&variable.should_execute>0
actions.standard+=/shield_slam
# [132155] :: Thunder Blast to apply Rend.
actions.standard+=/thunder_blast,if=talent.rend&(debuff.rend.down|variable.has_rend_threat=0)
# [132155] :: Thunder Clap to apply Rend.
actions.standard+=/thunder_clap,if=talent.rend&(debuff.rend.down|variable.has_rend_threat=0)
# free :: Execute before Sudden Death expires.
actions.standard+=/execute,if=talent.sudden_death&buff.sudden_death.remains<(buff.sudden_death.stack*gcd)&variable.should_execute>0
# free :: Revenge is free with Revenge!.
actions.standard+=/revenge,if=buff.revenge.up
actions.standard+=/call_action_list,name=outburst,if=talent.violent_outburst
actions.standard+=/call_action_list,name=ravager_up,if=talent.ravager&buff.shield_block.up&buff.ravager.remains>0
actions.standard+=/call_action_list,name=dump_rage
actions.standard+=/thunder_blast
# [132090] :: Revenge to apply Deep Wounds.
actions.standard+=/revenge,if=debuff.deep_wounds.down|variable.has_deep_wounds_threat=0
actions.standard+=/call_action_list,name=spend_rage,if=variable.should_ignore_pain=0&variable.should_spend_rage>0
actions.standard+=/thunder_clap
actions.standard+=/devastate
