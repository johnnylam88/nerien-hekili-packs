# Protection Warrior Priority List
# Based on *Protection Warrior Tank Guide* by Nomeratur
# [https://www.wowhead.com/guide/classes/warrior/protection/overview-pve-tank]
#
# Recommended Settings in `/hekili` > *Protection*:
#
# - Only Shockwave as Interrupt : **unused**
# - Overlap Shield Block : checked
# - Allow Stance Changes : unchecked
# - Reserve Rage for Mitigation : 0
# - Shield Wall Damage Required : 20
# - Shield Wall Health Percentage : 80
# - Rallying Cry Damage Required : 20
# - Rallying Cry Health Percentage : 80
# - Last Stand Damage Required : 20
# - Last Stand Health Percentage : 80
# - Victory Rush Health Threshold : 75

actions.precombat+=/variable,name=opener,value=1,op=set
actions.precombat+=/defensive_stance,if=!buff.defensive_stance.up
actions.precombat+=/battle_shout
actions.precombat+=/use_item,name=swarmlords_authority

actions+=/variable,name=rage_cost_execute,value=40
actions+=/variable,name=rage_cost_ignore_pain,value=35
actions+=/variable,name=rage_cost_revenge,value=20
actions+=/variable,name=rage_cost_shield_block,value=30

actions+=/variable,name=rage_gen_multiplier_outburst,value=2,op=setif,condition=buff.violent_outburst.up,value_else=1
actions+=/variable,name=rage_gain_shield_slam,value=(15+2*talent.heavy_repercussions+3*talent.impenetrable_wall+3*talent.practiced_strikes)*variable.rage_gen_multiplier_outburst
actions+=/variable,name=rage_gain_thunder_blast,value=10*variable.rage_gen_multiplier_outburst
actions+=/variable,name=rage_gain_thunder_clap,value=5*variable.rage_gen_multiplier_outburst

### Tunables ###

# Pool enough Rage to cast Shield Block with some cushion in case our Rage estimate is wrong.
actions+=/variable,name=rage_pool_value,value=variable.rage_cost_shield_block+20
# Don't cast Ignore Pain if we will waste more than 30% of the casted absorb.
actions+=/variable,name=ignore_pain_wasted_pct,value=30
# Demoralizing Shout tunable for incoming damage.
actions+=/variable,name=demoralizing_shout_damage_taken,value=0.2*health.max
# Last Stand tunable for incoming damage (see /hekili > Protection).
actions+=/variable,name=last_stand_damage_taken,value=last_stand_damage_taken
# Last Stand tunable for minimum health percent (see /hekili > Protection).
actions+=/variable,name=last_stand_health_pct,value=last_stand_health_pct
# Rallying Cry tunable for incoming damage (see /hekili > Protection).
actions+=/variable,name=rallying_cry_damage_taken,value=rallying_cry_damage_taken
# Rallying Cry tunable for minimum health percent (see /hekili > Protection).
actions+=/variable,name=rallying_cry_health_pct,value=rallying_cry_health_pct
# Shield Wall tunable for incoming damage (see /hekili > Protection).
actions+=/variable,name=shield_wall_damage_taken,value=shield_wall_damage_taken
# Spell Block tunable for incoming damage.
actions+=/variable,name=spell_block_damage_taken,value=0.2*health.max
# Victory Rush tunable for minimum health percent (see /hekili > Protection).
actions+=/variable,name=victory_rush_health_pct,value=victory_rush_health_pct

################


actions+=/variable,name=rage_gen_multiplier,value=1+0.5*buff.unnerving_focus.remains%buff.unnerving_focus.duration
actions+=/variable,name=rage_per_second_auto_attack,value=1*(1+0.5*talent.war_machine)+2*talent.instigate
# 3 Rage per incoming auto-attack, and incoming auto-attack every 2s with 1 mob.
# Assume at 5+ mobs, that we gain 3 Rage every second.
actions+=/variable,name=rage_per_second_incoming_auto_attack,value=(3%2)+((3%2)%(5-1))*(active_enemies-1),op=setif,condition=active_enemies<5,value_else=3
actions+=/variable,name=rage_per_second_incoming_damage,value=variable.rage_per_second_incoming_auto_attack,op=setif,condition=incoming_physical_damage_5s>0,value_else=0
# Bloodsurge has a 10% chance to gain 5 rage per Bleed tick.
actions+=/variable,name=rage_per_second_bloodsurge,value=talent.bloodsurge*(0.1*5%3)*active_dot.deep_wounds
actions+=/variable,name=rage_per_second_ravager,value=buff.ravager.up*5
# This underestimates the rage from Shield Slam because we don't account for Shield Slam resets.
actions+=/variable,name=rage_per_second_shield_slam,value=variable.rage_gain_shield_slam%cooldown.shield_slam.duration
# This underestimates the rage from Thunder Clap because we don't account for Thunder Blast.
actions+=/variable,name=rage_per_second_thunder_clap,value=variable.rage_gain_thunder_clap%cooldown.thunder_clap.duration
actions+=/variable,name=rage_per_second,value=(variable.rage_per_second_auto_attack+variable.rage_per_second_incoming_damage+variable.rage_per_second_bloodsurge+variable.rage_per_second_ravager+variable.rage_per_second_shield_slam+variable.rage_per_second_thunder_clap)*variable.rage_gen_multiplier
actions+=/variable,name=rage_at_next_shield_block,value=rage,op=setif,condition=cooldown.shield_block.charges>0,value_else=rage+cooldown.shield_block.remains*variable.rage_per_second
# should_spend_rage is 1 if the expected Rage by the next Shield Block exceeds the pool value.
actions+=/variable,name=should_spend_rage,value=1,op=setif,condition=variable.rage_at_next_shield_block>variable.rage_pool_value,value_else=0

actions+=/variable,name=rage_check,value=1,op=setif,condition=rage+variable.rage_per_second*gcd>rage.max,value_else=0
actions+=/variable,name=rage_check_avatar,value=1,op=setif,condition=talent.avatar&cooldown.avatar.remains<gcd&rage+10*variable.rage_gen_multiplier>rage.max,value_else=0
actions+=/variable,name=rage_check_champions_spear,value=1,op=setif,condition=talent.champions_spear&cooldown.champions_spear.remains<gcd&rage+20*variable.rage_gen_multiplier>rage.max,value_else=0
actions+=/variable,name=rage_check_demoralizing_shout,value=1,op=setif,condition=talent.demoralizing_shout&talent.booming_voice&cooldown.demoralizing_shout.remains<gcd&rage+30*variable.rage_gen_multiplier>rage.max,value_else=0
actions+=/variable,name=rage_check_shield_charge,value=1,op=setif,condition=talent.shield_charge&cooldown.shield_charge.remains<gcd&rage+(20+20*talent.champions_bulwark)*variable.rage_gen_multiplier>rage.max,value_else=0
actions+=/variable,name=rage_check_shield_slam,value=1,op=setif,condition=cooldown.shield_slam.remains<gcd&rage+variable.rage_gain_shield_slam*variable.rage_gen_multiplier>rage.max,value_else=0
actions+=/variable,name=rage_check_thunder_blast,value=1,op=setif,condition=talent.thunder_blast&cooldown.thunder_blast.remains<gcd&rage+variable.rage_gain_thunder_blast*variable.rage_gen_multiplier>rage.max,value_else=0
actions+=/variable,name=rage_check_thunder_clap,value=1,op=setif,condition=talent.thunder_clap&cooldown.thunder_clap.remains<gcd&rage+variable.rage_gain_thunder_clap*variable.rage_gen_multiplier>rage.max,value_else=0
# should_dump_rage is 1 if an ability is coming off cooldown and will overcap on Rage.
actions+=/variable,name=should_dump_rage,value=1,op=setif,condition=variable.rage_check>0|variable.rage_check_avatar>0|variable.rage_check_demoralizing_shout>0|variable.rage_check_shield_charge>0|variable.rage_check_shield_slam>0|variable.rage_check_champions_spear>0|variable.rage_check_thunder_blast>0|variable.rage_check_thunder_blast>0|variable.rage_check_thunder_clap>0,value_else=0

actions+=/variable,name=ignore_pain_cap,value=0.3*health.max
actions+=/variable,name=ignore_pain_absorb,value=buff.ignore_pain.v1,op=setif,condition=buff.ignore_pain.up,value_else=0
# Magic constants to estimate the absorb granted by casting Ignore Pain.
actions+=/variable,name=ignore_pain_absorb_oncast,value=stat.attack_power*4.375*(1+stat.versatility_atk_mod)*2
# should_ignore_pain is 1 if we are keeping up the Ignore Pain absorb or should dump Rage.
actions+=/variable,name=should_ignore_pain,value=1,op=setif,condition=(buff.ignore_pain.down|buff.ignore_pain.remains<gcd|(variable.ignore_pain_cap-variable.ignore_pain_absorb)>(variable.ignore_pain_absorb_oncast*(1-variable.ignore_pain_wasted_pct%100))&incoming_damage_5s>0)|variable.should_dump_rage>0,value_else=0

actions+=/variable,name=has_defensive_buff,value=1,op=setif,condition=active_dot.demoralizing_shout>0|buff.shield_wall.up|buff.spell_block.up|buff.last_stand.up|buff.rallying_cry.up

actions+=/pummel
actions+=/spell_reflection
actions+=/defensive_stance,if=!buff.defensive_stance.up&health.pct<70&incoming_damage_3s>0.2*health.max
actions+=/run_action_list,name=opener,if=time<5*gcd.max&variable.opener>0
actions+=/call_action_list,name=defensives,if=tanking
actions+=/call_action_list,name=heal
actions+=/battle_stance,if=!buff.battle_stance.up&health.pct>70
actions+=/call_action_list,name=mitigation
actions+=/run_action_list,name=standard

actions.defensives+=/use_item,name=swarmlords_authority,use_off_gcd=1
actions.defensives+=/demoralizing_shout,if=!talent.booming_voice&incoming_damage_5s>variable.demoralizing_shout_damage_taken&variable.has_defensive_buff=0
actions.defensives+=/shield_wall,use_off_gcd=1,if=(!talent.immovable_object|!cooldown.avatar.ready)&incoming_damage_5s>variable.shield_wall_damage_taken&variable.has_defensive_buff=0
actions.defensives+=/spell_block,if=incoming_magic_damage_5s>variable.spell_block_damage_taken&variable.has_defensive_buff=0
actions.defensives+=/last_stand,use_off_gcd=1,if=!talent.bolster&(incoming_damage_5s>variable.last_stand_damage_taken&health.pct<variable.last_stand_health_pct)&variable.has_defensive_buff=0
actions.defensives+=/rallying_cry,use_off_gcd=1,if=incoming_damage_5s>variable.rallying_cry_damage_taken&health.pct<variable.rallying_cry_health_pct&variable.has_defensive_buff=0

actions.heal+=/victory_rush,if=buff.victorious.up&health.pct<variable.victory_rush_health_pct
actions.heal+=/impending_victory,if=health.pct<variable.victory_rush_health_pct

actions.mitigation+=/shield_block,if=buff.shield_block.down|buff.shield_block.remains<4
actions.mitigation+=/last_stand,if=talent.bolster&buff.shield_block.down
# [236308] :: Ignore Pain to trigger Violent Outburst.
actions.mitigation+=/ignore_pain,if=buff.seeing_red.stack=7&cooldown.shield_slam.ready
actions.mitigation+=/ignore_pain,if=variable.should_ignore_pain>0

actions.opener+=/ravager
actions.opener+=/charge,if=movement.distance>8
actions.opener+=/avatar
actions.opener+=/shield_block,if=buff.shield_block.down|buff.shield_block.remains<4
actions.opener+=/last_stand,if=talent.bolster&buff.shield_block.down
actions.opener+=/ignore_pain,if=variable.should_dump_rage>0|(buff.seeing_red.stack=7&cooldown.shield_slam.ready)
actions.opener+=/demoralizing_shout,if=talent.booming_voice
actions.opener+=/champions_spear
actions.opener+=/thunderous_roar
actions.opener+=/shield_charge
actions.opener+=/call_action_list,name=outburst,if=talent.violent_outburst
actions.opener+=/thunder_blast,if=talent.unstoppable_force&buff.avatar.up
actions.opener+=/thunder_clap,if=talent.unstoppable_force&buff.avatar.up
actions.opener+=/shield_slam,if=buff.thunder_blast.stack<2
actions.opener+=/thunder_blast,if=buff.burst_of_power.stack<2
actions.opener+=/thunder_clap,if=buff.thunder_blast.down
actions.opener+=/variable,name=opener,value=0,op=set

# [236308] :: Thunder Blast to consume Violent Outburst.
actions.outburst+=/thunder_blast,if=buff.violent_outburst.up&spell_targets.thunder_clap>=2
# [236308] :: Thunder Clap to consume Violent Outburst.
actions.outburst+=/thunder_clap,if=buff.violent_outburst.up&(hero_tree.mountain_thane&spell_targets.thunder_clap>=4|spell_targets.thunder_clap>=10)
# [236308] :: Shield Slam to consume Violent Outburst.
actions.outburst+=/shield_slam,if=buff.violent_outburst.up
# [236308] :: Thunder Blast to consume Violent Outburst.
actions.outburst+=/thunder_blast,if=buff.violent_outburst.up&cooldown.shield_slam.remains>2
# [236308] :: Thunder Clap to consume Violent Outburst.
actions.outburst+=/thunder_clap,if=buff.violent_outburst.up&cooldown.shield_slam.remains>2
# [236308] :: Revenge to trigger Violent Outburst.
actions.outburst+=/revenge,if=variable.should_ignore_pain=0&(buff.seeing_red.stack=7&cooldown.shield_slam.remains>(gcd.remains+gcd.max))&spell_targets.revenge>1
# [236308] :: Execute to trigger Violent Outburst.
actions.outburst+=/execute,if=variable.should_ignore_pain=0&(buff.seeing_red.stack=7&cooldown.shield_slam.remains>(gcd.remains+gcd.max))

actions.racials+=/blood_fury
actions.racials+=/berserking
#actions.racials+=/arcane_torrent
actions.racials+=/lights_judgment
actions.racials+=/ancestral_call
#actions.racials+=/fireblood
actions.racials+=/bag_of_tricks

# [132090] :: Revenge to apply Deep Wounds
actions.ravager_up+=/revenge,if=debuff.deep_wounds.down
# [970854] :: Shield Slam during Ravager.
actions.ravager_up+=/shield_slam,if=talent.heavy_repercussions
# [970854] :: Revenge during Ravager.
actions.ravager_up+=/revenge

actions.spend_rage+=/execute,if=variable.rage_at_next_shield_block-variable.rage_cost_execute>=variable.rage_pool_value&spell_targets.revenge<=4
actions.spend_rage+=/revenge,if=variable.rage_at_next_shield_block-variable.rage_cost_revenge>=variable.rage_pool_value

actions.standard+=/charge,if=movement.distance>8
# [613534] :: Thunder Blast during Avatar.
actions.standard+=/thunder_blast,if=talent.unstoppable_force&buff.avatar.up
# [613534] :: Thunder Clap during Avatar.
actions.standard+=/thunder_clap,if=talent.unstoppable_force&buff.avatar.up
actions.standard+=/ravager
actions.standard+=/avatar
actions.standard+=/call_action_list,name=racials
actions.standard+=/potion,if=buff.avatar.up
actions.standard+=/demoralizing_shout,if=talent.booming_voice
actions.standard+=/demolish,if=buff.colossal_might.stack>=3
actions.standard+=/champions_spear
actions.standard+=/thunderous_roar
actions.standard+=/call_action_list,name=outburst,if=talent.violent_outburst
actions.standard+=/call_action_list,name=ravager_up,if=talent.ravager&buff.shield_block.up&buff.ravager.remains>0
actions.standard+=/shield_charge
actions.standard+=/shield_slam,if=buff.thunder_blast.stack<2
actions.standard+=/thunder_blast,if=buff.burst_of_power.stack<2
actions.standard+=/thunder_clap,if=buff.thunder_blast.down
# [132090] :: Revenge to apply Deep Wounds.
actions.standard+=/revenge,if=debuff.deep_wounds.down
# free :: Revenge is free with Revenge!.
actions.standard+=/revenge,if=buff.revenge.up
# free :: Execute is free with Sudden Death.
actions.standard+=/execute,if=talent.sudden_death&buff.sudden_death.up&spell_targets.revenge<=4
actions.standard+=/call_action_list,name=spend_rage,if=variable.should_ignore_pain=0&variable.should_spend_rage>0
actions.standard+=/devastate
