# Use either Abomination Limb or Death's Caress if you need to maintain Bone Shield between pulls.
actions.precombat+=/abomination_limb,if=buff.bone_shield.down|buff.bone_shield.remains<3
actions.precombat+=/use_item,name=fyrakks_tainted_rageheart
actions.precombat+=/use_item,name=leaf_of_the_ancient_protectors
actions.precombat+=/deaths_caress,if=buff.bone_shield.down|buff.bone_shield.remains<3
actions.precombat+=/blooddrinker,if=buff.dancing_rune_weapon.down
actions.precombat+=/death_and_decay,if=!death_and_decay.ticking|rune.time_to_6<gcd

# Bank Runic Power for three Death Strikes.
actions+=/variable,name=death_strike_pool_value,value=105
# Death Strike below 70% health.
actions+=/variable,name=death_strike_health_pct_threshold,value=70

# Dump Runic Power with Death Strike to avoid capping.
actions+=/variable,name=death_strike_dump_amount,value=variable.death_strike_pool_value,op=setif,condition=buff.dancing_rune_weapon.down,value_else=variable.death_strike_pool_value-20
# Refresh Bone Shield with Marrowrend to maintain high stack count.
actions+=/variable,name=bone_shield_refresh_value,value=8,op=setif,condition=(!talent.insatiable_blade.enabled|(buff.dancing_rune_weapon.down&cooldown.dancing_rune_weapon.remains>60))&(!talent.abomination_limb.enabled|(buff.abomination_limb.down&cooldown.abomination_limb.remains>20)),value_else=6

actions+=/mind_freeze
actions+=/strangulate
actions+=/call_action_list,name=defensives,if=tanking
actions+=/potion,if=buff.dancing_rune_weapon.up
actions+=/call_action_list,name=racials,if=buff.dancing_rune_weapon.up
actions+=/blooddrinker,if=buff.dancing_rune_weapon.down
# Use Blood Tap if you have 2 or fewer runes, you won't reach 4 charges within the global cooldown, and you're about to cap charges, or if you will not reach 3 runes within global cooldown.
actions+=/blood_tap,if=(rune<=2&rune.time_to_4>gcd&charges_fractional>=1.8)|rune.time_to_3>gcd
actions+=/empower_rune_weapon,if=rune<6&runic_power.deficit>5
actions+=/dancing_rune_weapon,if=buff.dancing_rune_weapon.down
actions+=/raise_dead
actions+=/call_action_list,name=core
actions+=/gorefiends_grasp,if=talent.tightening_grasp.enabled
actions+=/sacrificial_pact,if=buff.dancing_rune_weapon.down&(pet.ghoul.remains<3|boss&fight_remains<3)
actions+=/use_item,name=fyralath_the_dreamrender,if=dot.mark_of_fyralath.ticking&buff.dancing_rune_weapon.down

actions.core+=/death_strike,if=health.pct<variable.death_strike_health_pct_threshold
actions.core+=/abomination_limb
actions.core+=/marrowrend,if=buff.bone_shield.down|buff.bone_shield.remains<5|buff.bone_shield.stack<3
actions.core+=/blood_boil,if=!dot.blood_plague.ticking
actions.core+=/death_strike,if=runic_power>variable.death_strike_dump_amount
actions.core+=/soul_reaper,cycle_targets=1,if=target.time_to_pct_35<5&active_enemies<=2&target.time_to_die>(dot.soul_reaper.remains+5)
actions.core+=/marrowrend,if=buff.bone_shield.stack<variable.bone_shield_refresh_value
actions.core+=/tombstone,if=buff.bone_shield.stack>6&(!talent.shattering_bone.enabled|death_and_decay.ticking)&(!talent.dancing_rune_weapon.enabled|cooldown.dancing_rune_weapon.remains>15)
actions.core+=/death_and_decay,if=!death_and_decay.ticking
actions.core+=/heart_strike,if=rune.time_to_3<gcd
actions.core+=/blood_boil,if=charges_fractional>=1.8&buff.hemostasis.stack<5
actions.core+=/heart_strike,if=rune>=3|(buff.bone_shield.stack>=6&buff.bone_shield.remains>4.5)
actions.core+=/blood_boil,if=charges_fractional>=1.8|buff.hemostasis.stack<5
actions.core+=/consumption
actions.core+=/deaths_caress,if=buff.bone_shield.stack<9

actions.defensives+=/use_item,name=fyrakks_tainted_rageheart,use_off_gcd=1,if=incoming_damage_5s>=vb_damage&!(buff.dancing_rune_weapon.up|buff.vampiric_blood.up)
actions.defensives+=/use_item,name=leaf_of_the_ancient_protectors,use_off_gcd=1,if=incoming_damage_5s>=vb_damage&!(buff.dancing_rune_weapon.up|buff.vampiric_blood.up)
actions.defensives+=/icebound_fortitude,if=incoming_damage_5s>=ibf_damage&!(buff.dancing_rune_weapon.up|buff.vampiric_blood.up)
actions.defensives+=/vampiric_blood,if=incoming_damage_5s>=vb_damage&!(buff.dancing_rune_weapon.up|buff.icebound_fortitude.up|buff.vampiric_blood.up|buff.vampiric_strength.up)
actions.defensives+=/rune_tap,if=incoming_damage_5s>=rt_damage&!(buff.dancing_rune_weapon.up|buff.vampiric_blood.up|buff.icebound_fortitude.up)

actions.racials=blood_fury,if=cooldown.dancing_rune_weapon.ready&(!cooldown.blooddrinker.ready|!talent.blooddrinker.enabled)
actions.racials+=/berserking
actions.racials+=/arcane_pulse,if=active_enemies>=2|rune<1&runic_power.deficit>60
actions.racials+=/lights_judgment,if=buff.unholy_strength.up
actions.racials+=/ancestral_call
#actions.racials+=/fireblood
actions.racials+=/bag_of_tricks
#actions.racials+=/arcane_torrent,if=runic_power.deficit>20
