# Blood Death Knight (Kyrasis) Action Priority List
# Based on *Advanced Blood Death Knight Guide for M+*
# [https://docs.google.com/document/d/1FJlB1T8ijaQLjY_cihyoyhLoi6lYRnT-N-ipVFCLidE]
#
# NOTE: Using this profile with Hekili requires using the actual cooldown of
#       *cooldown* spells. This may be set in `/hekili` > *Abilities* and
#       checking the option:
#       *[GLOBAL] When Cooldowns Show Separately, Use Actual Cooldown*
#
# NOTE: It is recommended to adjust the settings in `/hekili` > *Blood*
#       so that the *Damage Threshold* sliders are set to *20%*.

actions.precombat+=/abomination_limb
actions.precombat+=/raise_dead
actions.precombat+=/rune_tap,if=buff.rune_tap.down&rune.time_to_6<gcd
actions.precombat+=/death_and_decay,if=!death_and_decay.ticking|rune.time_to_6<gcd
actions.precombat+=/deaths_caress,if=buff.bone_shield.down|buff.bone_shield.remains<3

### Tunables ###

# Bank Runic Power for three Death Strikes.
actions+=/variable,name=death_strike_pool_value,value=105
# Death Strike below 70% health.
actions+=/variable,name=death_strike_health_pct_threshold,value=70
# Death Strike only if the damage taken events exceed 28% of maximum health.
actions+=/variable,name=death_strike_damage_taken_pct_threshold,value=28

################

# Dump Runic Power with Death Strike to avoid capping.
actions+=/variable,name=death_strike_dump_amount,value=variable.death_strike_pool_value,op=setif,condition=buff.dancing_rune_weapon.down,value_else=variable.death_strike_pool_value-10

actions+=/variable,name=death_strike_heal_multiplier,value=(1+0.05*talent.improved_death_strike)*(1+0.15*talent.voracious)*(1+0.08*buff.hemostasis.stack)
# Minimum Death Strike heal is 7% of maximum health.
actions+=/variable,name=death_strike_heal_pct,value=(7<?25*(buff.coagulating_blood.stack%100))*variable.death_strike_heal_multiplier
actions+=/variable,name=death_strike_heal_amount,value=health.max*variable.death_strike_heal_pct

# has_defensive_buff is 1 if Vampiric Blood, Dancing Rune Weapon, Icebound Fortitude, Rune Tap, or Lichborne are active.
actions+=/variable,name=has_defensive_buff,value=1,op=setif,condition=buff.vampiric_blood.up|buff.dancing_rune_weapon.up|buff.icebound_fortitude.up|buff.rune_tap.up|(talent.unholy_endurance&buff.lichborne.up),value_else=0

actions+=/mind_freeze
actions+=/strangulate
actions+=/gorefiends_grasp,if=talent.tightening_grasp
actions+=/call_action_list,name=defensives,if=tanking&variable.has_defensive_buff=0
actions+=/potion,if=buff.dancing_rune_weapon.up
actions+=/call_action_list,name=racials,if=buff.dancing_rune_weapon.up
actions+=/run_action_list,name=core

actions.core+=/vampiric_blood,if=buff.vampiric_blood.down&talent.gift_of_the_sanlayn
actions.core+=/raise_dead
actions.core+=/blood_tap,if=(rune<=2&rune.time_to_3>gcd&full_recharge_time<2*gcd)|(rune<=1&rune.time_to_3>gcd)
actions.core+=/death_strike,if=health.pct<variable.death_strike_health_pct_threshold&buff.coagulating_blood.stack>variable.death_strike_damage_taken_pct_threshold
actions.core+=/dancing_rune_weapon,if=buff.dancing_rune_weapon.down
actions.core+=/marrowrend,if=buff.bone_shield.down|buff.bone_shield.remains<5|buff.bone_shield.stack<3|(talent.reapers_mark&(buff.exterminate.up|buff.exterminate_painful_death.up)&cooldown.reapers_mark.remains<3)
actions.core+=/blood_boil,if=dot.blood_plague.remains<3
actions.core+=/heart_strike,if=buff.gift_of_the_sanlayn.up&((buff.essence_of_the_blood_queen.up&buff.essence_of_the_blood_queen.remains<2*gcd)|(buff.gift_of_the_sanlayn.remains<2*gcd&!prev_gcd.heart_strike))
actions.core+=/death_strike,if=runic_power>variable.death_strike_dump_amount
actions.core+=/sacrificial_pact,if=buff.dancing_rune_weapon.down&(pet.ghoul.remains<3|boss&fight_remains<3)
actions.core+=/reapers_mark
actions.core+=/bonestorm,if=buff.bone_shield.stack>6&(!talent.shattering_bone|death_and_decay.ticking)&(!talent.dancing_rune_weapon|!talent.insatiable_blade|cooldown.dancing_rune_weapon.remains>25)
actions.core+=/soul_reaper,cycle_targets=1,if=target.time_to_pct_35<5&active_enemies<=2&target.time_to_die>(dot.soul_reaper.remains+5)
actions.core+=/marrowrend,if=(((talent.gift_of_the_sanlayn&buff.bone_shield.stack<7)|buff.bone_shield.stack<6)&buff.bonestorm.down)|((buff.exterminate.up|buff.exterminate_painful_death.up)&debuff.reapers_mark.up&(debuff.reapers_mark.remains<5|debuff.reapers_mark.stack>=20))
actions.core+=/consumption,if=(talent.reapers_mark&buff.coagulopathy.stack>=4)|buff.infliction_of_sorrow.up
actions.core+=/tombstone,if=buff.bone_shield.stack>8&(!talent.shattering_bone|death_and_decay.ticking)&(!talent.dancing_rune_weapon|!talent.insatiable_blade|cooldown.dancing_rune_weapon.remains>25)
actions.core+=/abomination_limb
actions.core+=/death_and_decay,if=!death_and_decay.ticking
actions.core+=/marrowrend,if=(buff.exterminate.up|buff.exterminate_painful_death.up)&buff.dancing_rune_weapon.down
actions.core+=/blooddrinker,if=buff.dancing_rune_weapon.down&active_enemies<=2
actions.core+=/blood_boil,if=talent.infliction_of_sorrow&dot.blood_plague.remains<15
actions.core+=/heart_strike,if=rune>=2
actions.core+=/consumption,if=talent.infliction_of_sorrow|!talent.coagulopathy
actions.core+=/blood_boil
actions.core+=/heart_strike
actions.core+=/deaths_caress,if=buff.bone_shield.stack<11

#actions.defensives+=/antimagic_shell,if=incoming_magic_damage_5s>=0.2*health.max
actions.defensives+=/vampiric_blood,if=buff.vampiric_blood.down&incoming_damage_5s>=vb_damage&!talent.gift_of_the_sanlayn
actions.defensives+=/icebound_fortitude,if=incoming_damage_5s>=ibf_damage
actions.defensives+=/rune_tap,if=buff.rune_tap.down&incoming_damage_5s>=rt_damage
#actions.defensives+=/antimagic_zone,if=incoming_magic_damage_5s>=0.2*health.max
actions.defensives+=/lichborne,if=talent.unholy_endurance&incoming_damage_5s>=0.2*health.max

actions.racials=blood_fury,if=cooldown.dancing_rune_weapon.ready&(!cooldown.blooddrinker.ready|!talent.blooddrinker)
actions.racials+=/berserking
actions.racials+=/arcane_pulse,if=active_enemies>=2|rune<1&runic_power.deficit>60
actions.racials+=/lights_judgment,if=buff.unholy_strength.up
actions.racials+=/ancestral_call
#actions.racials+=/fireblood
actions.racials+=/bag_of_tricks
#actions.racials+=/arcane_torrent,if=runic_power.deficit>20
